//===== rAthena Script =======================================
//= Eden Group Quest - Quests NPCs
//===== Description: =========================================
//= Eden Group Headquarter NPCs.
//===== Changelogs: ==========================================
//= 1.0 First Version. [L0ne_W0lf]
//= 1.1 Removed unencoded comments (Korean -> Gibberish)
//=     Readded the GM helper NPC, commented out.
//= 1.2 Some little optimization here and there. [Masao]
//= 1.3 Added Instructor Ur and the new Quests which come
//=     alongside him. Special thanks to Chilly for the base. [Masao]
//= 1.4 Partial cleaning and bug fixing. [Euphy]
//= 1.4a Added 'npcskill' command. [Euphy]
//= 1.4b Added 'disable_items' command. [Euphy]
//= 1.5 Added GM management function. [Euphy]
//= 1.6 Kagerou/Oboro support (Class -> BaseClass) [Euphy]
//= 2.0 Updated Instructor Ur's quests. [Capuche]
//= 2.1 Updated Blacksmith Thorn and Weapons Expert NPC. [Capuche]
//= 2.2 Added support to Para_Team_Mark_ in Eden Group. [Ragno]
//= 2.3 Added use of F_HasEdenGroupMark function. [Ragno]
//============================================================
moc_para01,112,96,4	script	[ Administrator ]	4_M_RUSMAN1,{
	mes "[Michael]";
	mes "Why did you come here?";
	next;
	switch (select("To get supplies:Where is here?:Upgrade equipment")) {
		mes "[Michael]";
	case 1:
		if (para_suv01 == 11) {
			mes "If you've completed step 1";
			mes "we can supply you with a Eden Group Hat, Uniform, Manteau and Boots.";
			mes "^4d4dffCheck your inventory first.^000000";
			next;
			if (select("Let me check my inventory:I have enough room.") == 1) {
				mes "[Michael]";
				mes "Make sure you have enough room for the supplies.";
				close;
			}
			mes "[Michael]";
			mes "Two of the supplies, the ^4d4dffHat and Manteau^000000,";
			mes "will only be given out once.";
			mes "So treat them with caution and care.";
			next;
			mes "[Michael]";
			mes "As for the Boots and the Uniforms, you will receive better quality ones based on your course grades.";
			next;
			mes "[Michael]";
			mes "1 " + mesitemicon(5583) + "Eden Group Hat";
			mes "1 " + mesitemicon(2560) + "Eden Group Manteau";
			mes "1 " + mesitemicon(2456) + "Eden Group Boots I";
			mes "1 " + mesitemicon(15009) + "Eden Group Uniform I";
			mes "A total of 4 supplies, that's all.";
			para_suv01 = 12;
			para_suv02 = 1;
			getitem 5583,1; //Para_Team_Hat1
			getitem 2560,1; //Para_Team_Manteau1
			getitem 2456,1; //Para_Team_Boots1
			getitem 15009,1; //Para_Team_Uniform1
			next;
			mes "[Michael]";
			mes "Is that correct?";
			mes "It is manufactured for beginners so they don't have the best effect but they're still cheaper than equipment in the shops.";
			next;
			mes "[Michael]";
			mes "We made them especially for the Eden Group.";
		}
		else if (para_suv01 == 22) {  
			mes "If you've completed step 2, we offer extra weapons including the basic equipment.";  
			next;  
			mes "[Michael]";  
			mes "We have chosen the proper weapon for each class.";  
			mes "But we can't support some classes that can't join us.";  
			next;  
			mes "[Michael]";  
			mes "We can't manufacture all of the weapons in the world, don't you agree?";  
			next;  
			mes "[Michael]";  
			mes "We will supply 1 Weapon, Shoes and Uniform so a total of three things.";  
			mes "Also we supply extra things to consider some members who can't use some of the supplies.";  
			next;  
			mes "[Michael]";  
			mes "^4d4dffPlease check your inventory to get those items.^000000";  
			next;  
			if (select("I will make more space.:I have got enough space.") == 1) {  
				mes "[Michael]";  
				mes "Make enough space.";  
				close;  
			}  
			mes "[Michael]";  
			  
			// Class-based weapon distribution with icons  
			if (Class == Job_Swordman || Class == Job_Swordman_High || Class == Job_Knight || Class == Job_Crusader) {  
				mes "Available weapons for your class:";  
				mes mesitemicon(1192) + " Eden Slayer I (Two-handed sword)";  
				mes mesitemicon(13423) + " Eden Saber I (One-handed sword)";  
				next;  
				callsub S_Select,"We have one and Two-handed swords",  
					1192,"Eden Slayer I","Two-handed sword","Attack 162","",  
					13423,"Eden Saber I","One-handed sword","Attack 147","This is what we strive for.";  
			}  
			else if (Class == Job_Thief || Class == Job_Thief_High || Class == Job_Assassin || Class == Job_Rogue || Class == Job_Ninja || Class == Job_Novice || Class == Job_Novice_High) {  
				mes "Available weapon for your class:";  
				mes mesitemicon(13050) + " Eden Dagger I";  
				next;  
				callsub S_Select,"",  
					13050,"Eden Dagger I","Dagger","MATK+60, attack 124","This is what we strive for.";  
			}  
			else if (Class == Job_Merchant || Class == Job_Merchant_High || Class == Job_Blacksmith || Class == Job_Alchemist) {  
				mes "Available weapons for your class:";  
				mes mesitemicon(13423) + " Eden Saber I (One-handed sword)";  
				mes mesitemicon(16004) + " Eden Mace I";  
				next;  
				callsub S_Select,"We have a mace and a One-handed sword",  
					13423,"Eden Saber I","One-handed sword","Attack 147","This is what we strive for.",  
					16004,"Eden Mace I","Mace","Attack 142","";  
			}  
			else if (Class == Job_Archer || Class == Job_Archer_High || Class == Job_Hunter || Class == Job_Dancer || Class == Job_Bard) {  
				mes "Available weapon for your class:";  
				mes mesitemicon(1747) + " Eden Bow I";  
				next;  
				callsub S_Select,"",  
					1747,"Eden Bow I","Bow","Attack 82","";  
			}  
			else if (Class == Job_Acolyte || Class == Job_Acolyte_High || Class == Job_Priest || Class == Job_Monk) {  
				mes "Available weapons for your class:";  
				mes mesitemicon(16004) + " Eden Mace I";  
				mes mesitemicon(1650) + " Eden Staff I";  
				next;  
				callsub S_Select,"We have a mace and a staff",  
					16004,"Eden Mace I","Mace","Attack 142","",  
					1650,"Eden Staff I","Staff","INT+2, MATK+125, attack 60","";  
			}  
			else if (Class == Job_Mage || Class == Job_Mage_High || Class == Job_Wizard || Class == Job_Sage) {  
				mes "Available weapon for your class:";  
				mes mesitemicon(1650) + " Eden Staff I";  
				next;  
				callsub S_Select,"",  
					1650,"Eden Staff I","Staff","INT+2, MATK+125, attack 60","";  
			}  
			else if (Class == Job_Gunslinger) {  
				mes "Available weapon for your class:";  
				mes mesitemicon(13112) + " Eden Revolver I";  
				next;  
				callsub S_Select,"",  
					13112,"Eden Revolver I","Revolver","HIT-5, attack 44","";  
			}  
			else if (Class == Job_Summoner) {  
				mes "Available weapon for your class:";  
				mes mesitemicon(1699) + " Eden Foxtail Staff I";  
				next;  
				callsub S_Select,"",  
					1699,"Eden Foxtail Staff I","Staff","MATK+135, attack 120","";  
			}  
			else if (Class == Job_Taekwon) {  
				mes "Available weapon for your class:";  
				mes mesitemicon(1583) + " Eden Book I";  
				next;  
				callsub S_Select,"",  
					1583,"Eden Book I","Book","MATK+110, attack 135","";  
			}  
			else {  
				mes "I don't know what weapon will suit you so, you'll get a Dagger.";  
				mes mesitemicon(13050) + " Eden Dagger I";  
				getitem 13050,1; // P_Dagger1  
			}  
			  
			mes "Let me see... you will receive..";  
			mes "Equipment package includes:";  
			mes mesitemicon(2457) + " Eden Group Boots II";  
			mes mesitemicon(15010) + " Eden Group Uniform II";  
			next;  
			mes "[Michael]";  
			para_suv01 = 23;  
			getitem 2457,1; // Para_Team_Boots2  
			getitem 15010,1; // Para_Team_Uniform2  
			  
			if (para_suv02 == 0) {  
				mes "You don't have a record of receiving any supplies";  
				mes "so, you'll receive additional items:";  
				mes mesitemicon(5583) + " Eden Group Hat";  
				mes mesitemicon(2560) + " Eden Group Manteau";  
				getitem 5583,1; // Para_Team_Hat  
				getitem 2560,1; // Para_Team_Manteau  
				para_suv02 = 2;  
				next;  
				mes "[Michael]";  
				mes "Check your supplies again and look after it.";  
				close;  
			}  
			para_suv02 = 2;  
			mes "A Weapon, Uniform and Boots all 3 supplies.";  
			mes "Please check it again.";  
			next;  
			mes "[Michael]";  
			mes "I hope they're useful to you.";  
		}
		else if (para_suv01 == 37) {  
			mes "You've completed the last training course.";  
			mes "It's time for you to receive a new weapon.";  
			next;  
			mes "[Michael]";  
			mes "We have more upgraded weapons, uniforms and boots.";  
			mes "Ah, in case of the weapon that was made only for 1st jobs.";  
			mes "So, I can't offer them to higher jobs.";  
			next;  
			mes "[Michael]";  
			mes "^4d4dffPlease check your inventory to get those supplies.^000000";  
			next;  
			if (select("I'll come back.:I have enough room.") == 1) {  
				mes "[Michael]";  
				mes "Make sure you have enough room.";  
				close;  
			}  
			mes "[Michael]";  
			  
			// Class-based weapon distribution with icons (Tier II equipment)  
			if (Class == Job_Swordman || Class == Job_Swordman_High || Class == Job_Knight || Class == Job_Crusader) {  
				mes "Available upgraded weapons for your class:";  
				mes mesitemicon(13424) + " Eden Saber II (One-handed sword)";  
				mes mesitemicon(1193) + " Eden Slayer II (Two-handed sword)";  
				next;  
				callsub S_Select,"We have one and Two-handed swords",  
					13424,"Eden Saber II","One-handed sword","Attack 170","",  
					1193,"Eden Slayer II","Two-handed sword","Attack 185","";  
			}  
			else if (Class == Job_Thief || Class == Job_Thief_High || Class == Job_Assassin || Class == Job_Rogue || Class == Job_Novice || Class == Job_Novice_High || Class == Job_Super_Novice || Class == Job_Soul_Linker || Class == Job_Ninja) {  
				mes "Available upgraded weapon for your class:";  
				mes mesitemicon(13051) + " Eden Dagger II";  
				next;  
				callsub S_Select,"",  
					13051,"Eden Dagger II","Dagger","MATK+70, attack 158","";  
			}  
			else if (Class == Job_Acolyte || Class == Job_Acolyte_High || Class == Job_Priest || Class == Job_Monk) {  
				mes "Available upgraded weapons for your class:";  
				mes mesitemicon(16005) + " Eden Mace II";  
				mes mesitemicon(1651) + " Eden Staff II";  
				next;  
				callsub S_Select,"We have a mace and a staff",  
					16005,"Eden Mace II","Mace","Attack 163","",  
					1651,"Eden Staff II","Staff","INT+3, MATK+150, attack 60","";  
			}  
			else if (Class == Job_Archer || Class == Job_Archer_High || Class == Job_Hunter || Class == Job_Dancer || Class == Job_Bard) {  
				mes "Available upgraded weapon for your class:";  
				mes mesitemicon(1748) + " Eden Bow II";  
				next;  
				callsub S_Select,"",  
					1748,"Eden Bow II","Bow","Attack 82","";  
			}  
			else if (Class == Job_Mage || Class == Job_Mage_High || Class == Job_Wizard || Class == Job_Sage) {  
				mes "Available upgraded weapon for your class:";  
				mes mesitemicon(1651) + " Eden Staff II";  
				next;  
				callsub S_Select,"",  
					1651,"Eden Staff II","Staff","INT+3, MATK+155, attack 60","";  
			}  
			else if (Class == Job_Merchant || Class == Job_Merchant_High || Class == Job_Blacksmith || Class == Job_Alchemist) {  
				mes "Available upgraded weapons for your class:";  
				mes mesitemicon(13424) + " Eden Saber II (One-handed sword)";  
				mes mesitemicon(16005) + " Eden Mace II";  
				next;  
				callsub S_Select,"We have a mace and a One-handed sword",  
					13424,"Eden Saber II","One-handed sword","Attack 170","",  
					16005,"Eden Mace II","Mace","Attack 163","";  
			}  
			else if (Class == Job_Gunslinger) {  
				mes "Available upgraded weapon for your class:";  
				mes mesitemicon(13113) + " Eden Revolver II";  
				next;  
				callsub S_Select,"",  
					13113,"Eden Revolver II","Revolver","HIT-5, attack 60","";  
			}  
			else if (Class == Job_Summoner) {  
				mes "Available upgraded weapon for your class:";  
				mes mesitemicon(26100) + " Eden Foxtail Staff II";  
				next;  
				callsub S_Select,"",  
					26100,"Eden Foxtail Staff II","Staff","MATK+165, attack 135","";  
			}  
			else {  
				// Fallback for unsupported classes  
				mes "I don't have upgraded weapons for your class.";  
				mes "You'll receive the standard equipment package only.";  
			}  
			  
			mes "Let me see... you will receive..";  
			mes "Upgraded equipment package includes:";  
			mes mesitemicon(2458) + " Eden Group Boots III";  
			mes mesitemicon(15011) + " Eden Group Uniform III";  
			next;  
			para_suv01 = 38;  
			getitem 2458,1; // Para_Team_Boots3  
			getitem 15011,1; // Para_Team_Uniform3  
			mes "[Michael]";  
			  
			if (para_suv02 == 0) {  
				mes "You don't have a record of receiving any supplies";  
				mes "so, you'll receive additional items:";  
				mes mesitemicon(5583) + " Eden Group Hat";  
				mes mesitemicon(2560) + " Eden Group Manteau";  
				para_suv02 = 3;  
				getitem 5583,1; // Para_Team_Hat  
				getitem 2560,1; // Para_Team_Manteau  
				next;  
				mes "[Michael]";  
				mes "Check your supplies again and look after it.";  
				close;  
			}  
			para_suv02 = 3;  
			mes "A Uniform and Boots all 2 supplies.";  
			mes "Please check it again.";  
			next;  
			mes "[Michael]";  
			mes "I hope they're useful to you.";  
		}
		else {
			mes "Wait...I will check the record...";
			mes "...";
			mes "...hummmm.";
			next;
			mes "[Michael]";
			mes "Sorry, but I can't find any record that you can obtain supplies.";
			mes "Are you sure?";
		}
		close;
	case 2:
		mes "We store weapons, armor and other goods which were created by the Eden Group here.";
		mes "We also have a lot of special stuff.";
		next;
		mes "[Michael]";
		mes "To prepare for emergencies, we hav enough equipment and supplies for an entire army.";
		mes "Frankly... we don't have a use for it now but in case soemthing happens like in Morocc.";
		next;
		mes "[Michael]";
		mes "Just take a look around and don't touch anything.";
		next;
		mes "[Michael]";
		mes "If I make a mistake, Reke will punish me.";
		close;
	case 3:
		mes "You mean upgrading equipment, right?";
		mes "We can only upgrade the Eden Group Hat.";
		next;
		mes "[Michael]";
		if (para_suv02 == 3) {
			if (countitem(5583) > 0) {
				disable_items;
				mes "What status bonus do you want to upgrade?";
				next;
				switch (select("Upgrade STR:Upgrade AGI:Upgrade VIT:Upgrade INT:Upgrade DEX:Upgrade LUK:Nevermind.")) {
				case 1: callsub S_Upgrade,"STR",4701;
				case 2: callsub S_Upgrade,"AGI",4731;
				case 3: callsub S_Upgrade,"VIT",4741;
				case 4: callsub S_Upgrade,"INT",4711;
				case 5: callsub S_Upgrade,"DEX",4721;
				case 6: callsub S_Upgrade,"LUK",4751;
				case 7:
					mes "[Michael]";
					mes "Why? It'll be beter than it is.";
					mes "Anyway, we can only offer you one Hat.";
					next;
					mes "[Michael]";
					mes "What you do with it is up to you.";
					close;
				}
			}
			mes "First come with a Hat that you want me to upgrade.";
			mes "Make sure that it's in your inventory, got it?";
			close;
		}
		if (para_suv02 == 4) {
			mes "Umm, didn't you upgrade this already?";
			mes "According to the records";
			mes ""+strcharinfo(0)+": Has already upgraded their Hat.";
			next;
			mes "[Michael]";
			mes "We can only offer 1 upgrade.";
			mes "Sorry but I can't do it twice.";
			close;
		}
		mes "You haven't received all the supplies up to step 3.";
		mes "Upgrading your Hat is a special service.";
		next;
		mes "[Michael]";
		mes "Sorry but I can't help you.";
		close;
	}
S_Upgrade:
	mes "[Michael]";
	mes "I see.";
	mes "I will ^4d4dffUpgrade "+ getarg(0) +"^000000.";
	mes "Are you sure?";
	next;
	switch (select("Yes I am.:No wait.")) {
	case 1:
		mes "[Michael]";
		mes "I will start to upgrade.";
		next;
		mes "[Michael]";
		mes "Here you are.";
		para_suv02 = 4;
		delitem 5583,1;
		getitem2 5583,1,1,0,0,0,0,0,getarg(1);
		close;
	case 2:
		mes "[Michael]";
		mes "Don't you want to upgrade?";
		close;
	}

S_Select:
	.@total_arg = getargcount();
	if (.@total_arg < 7)
		mes "We only have 1 weapon for you.";
	else {
		mes "What kind of weapon do you want?";
		mes getarg(0);
		mes "Here are the options.";
	}
	next;
	mes "[Michael]";
	for ( .@i = 1; .@i < .@total_arg; .@i += 5 ) {
		set .@menu$, .@menu$ + getarg(.@i+1) + ":";
		mes getarg(.@i+1)+": "+ getarg(.@i+2) +". "+ getarg(.@i+3) +".";
	}
	if (.@total_arg < 7)
		mes "It is Lv. 2 and the required level is "+ (para_suv01 == 22 ? "26" : "40") +".";
	else
		mes "Both of them are Lv. 2 weapons and the required level is "+ (para_suv01 == 22 ? "26" : "40") +".";
	next;
	mes "[Michael]";
	mes "They also can't be traded with other players or be refined.";
	next;
	if (.@total_arg < 7) {
		.@i = 1;
		mes "[Michael]";
		mes "You'll receive the "+ getarg(.@i+1) +".";
		next;
		mes "[Michael]";
	}
	else {
		.@i = (select(.@menu$) -1) *5 +1;
		mes "[Michael]";
		mes "You've chosen the "+ getarg(.@i+1) +".";
	}
	if (para_suv01 == 22) {
		mes "Additionally you'll receive the " + mesitemicon(2457) + " Eden Group Boots II and " + mesitemicon(15010) + " Uniform II.";
		next;
		getitem 2457,1;	// Para_Team_Boots2
		getitem 15010,1;// Para_Team_Uniform2
		para_suv01 = 23;
	}
	else {
		mes "Additionally you'll receive the " + mesitemicon(2458) + " Eden Group Boots III and " + mesitemicon(15011) + " Uniform III.";
		next;
		getitem 2458,1;// Para_Team_Boots3
		getitem 15011,1;// Para_Team_Uniform3
		para_suv01 = 38;
	}
	getitem getarg(.@i),1;
	mes "[Michael]";
	if (para_suv02 == 0) {
		mes "You don't have a record of receiving any supplies";
		mes "so, you'll receive the " + mesitemicon(5583) + " Eden Group Hat and " + mesitemicon(2560) + " Manteau aswell.";
		getitem 5583,1;// Para_Team_Hat
		getitem 2560,1;// Para_Team_Manteau
		if (para_suv01 == 22)
			para_suv02 = 2;
		else
			para_suv02 = 3;
		next;
		mes "[Michael]";
		mes "Check your supplies again and look after it.";
	}
	else {
		if (para_suv01 == 22)
			para_suv02 = 2;
		else
			para_suv02 = 3;
		mes "A "+ getarg(.@i+2) +", Uniform and Boots all 3 supplies.";
		mes "Please check it again.";
		next;
		mes "[Michael]";
		mes "I hope they're useful to you.";
		mes getarg(.@i+4);
	}
	close;
OnInit:
	setunitdata( getnpcid(0), UNPC_GROUP_ID,6 );
	setunittitle( getnpcid(0), "Michael" );
	end;	
}

moc_para01,179,44,3	script	Chef	820,{
	mes "[Chef]";
	mes "What's up?";
	mes "Do you want a Meal? Or do you have other business?";
	next;
	switch (select("Order a meal.:Talk.")) {
	case 1:
		mes "[Chef]";
		mes "Choose one of the three course meals A, B or C.";
		mes "Do you want a explanation?";
		next;
		switch (select("I want a explanation.:Order course meal A:Order course meal B:Order course meal C:End Conversation.")) {
		case 1:
			mes "[Chef]";
			mes "Uh? what do you want to know?";
			next;
			switch (select("About course meal A.:About course meal B.:About course meal C.:End Conversation.")) {
			case 1:
				mes "[Chef]";
				mes "Course meal A is for nomal people.";
				mes "It has three kinds of dishes and the main is....";
				mes " ";
				mes "-Chef is trying to point to a dish in the picture. There is a black roll.-";
				next;
				mes "[Chef]";
				mes "It's made of sea grass so, it's dry like paper.";
				mes "It's a roll with steamed rice, vegetables and meat inside.";
				mes "It seems simple but it is really good and healthy.";
				next;
				mes "[Chef]";
				mes "The ingredients mix well with the spicy sauce.";
				mes "It is also mixed with chopped meat like sausages.";
				next;
				mes "[Chef]";
				mes "It's simple and cheap so it is really popular with everyone.";
				mes "Just 3,000 Zeny.";
				mes "You will feel satisfied after eating it.";
				close;
			case 2:
				mes "[Chef]";
				mes "Um course meal B is.";
				mes "I make a sauce with aromatic vegetables and meat in a soup.";
				next;
				mes "[Chef]";
				mes "The meat is boiled so it is fork tender in the soup.";
				mes "When the soup is almost done I add noodles for the finishing touch.";
				next;
				mes "[Chef]";
				mes "It's a good dish to share with your friends.";
				mes "It's 4,000 Zeny.";
				mes "It's a very fun dish to enjoy.";
				close;
			case 3:
				mes "[Chef]";
				mes "Now for course meal C...";
				mes "It's a masterpiece of meat... Legend of the meat class!";
				next;
				mes "[Chef]";
				mes "Beef, bacon,";
				mes "strip loin,";
				mes "rib eye roll";
				mes "...";
				next;
				mes "[Chef]";
				mes "Do you need more information?";
				mes "Don't worry.";
				mes "I use the besk oak to smoke it.";
				mes "Ah, it's 5,000 Zeny. Cheap isn't it? I only use the best meat so don't worry.";
				close;
			case 4:
				mes "[Chef]";
				mes "I don't have enough time to chat with you...";
				close;
			}
		case 2:
			mes "[Chef]";
			mes "Course meal A?";
			mes "Ah, Kim-dduck-soon.";
			mes "It's the representative meal for normal citizens.";
			next;
			if (callfunc("F_HasEdenGroupMark")) {
				if (Zeny > 2699) {
					mes "[Chef]";
					mes "Hey, here you are.";
					mes "Enjoy your meal.";
					next;
					mes "- After eating the meal, You feel a little bit full.";
					mes "You've recovered some HP and SP. -";
					set Zeny, Zeny - 2700;
					percentheal 50,0;
					percentheal 0,50;
					close;
				}
				mes "[Chef]";
				mes "This meal costs 3,000 zeny but I will serve it for 2,700.";
				mes "Since you are a eden member you get a 10% discount so, it's 2,700 zeny.";
				close;
			}
			if (Zeny > 2999) {
				mes "[Chef]";
				mes "Hey, here you are.";
				mes "Enjoy your meal.";
				next;
				mes "- After eating the meal, You feel a little bit full.";
				mes "You've recovered some HP and SP. -";
				set Zeny, Zeny - 3000;
				percentheal 50,0;
				percentheal 0,50;
				close;
			}
			mes "[Chef]";
			mes "This meal costs 3,000 zeny.";
			mes "How many times do I have to tell you?";
			close;
		case 3:
			mes "[Chef]";
			mes "Course meal B?";
			mes "This food with meat and vegetables in hot soup has it's origins from nomadic life under the cold and dry nature.";
			next;
			if (callfunc("F_HasEdenGroupMark")) {
				if (Zeny > 3599) {
					mes "[Chef]";
					mes "Hey, here you are.";
					mes "Enjoy your meal.";
					next;
					mes "- After eating the meal, You feel a little bit full.";
					mes "You've recovered some HP and SP. -";
					set Zeny, Zeny - 3600;
					percentheal 75,0;
					percentheal 0,75;
					close;
				}
				mes "[Chef]";
				mes "This meal costs 4,000 zeny but I will serve it for 3,600.";
				mes "Since you are a eden member you get a 10% discount so, it's 3,600 zeny.";
				close;
			}
			if (Zeny > 3999) {
				mes "[Chef]";
				mes "Hey, here you are.";
				mes "Enjoy your meal.";
				next;
				mes "- After eating the meal, You feel a little bit full.";
				mes "You've recovered some HP and SP. -";
				set Zeny, Zeny - 4000;
				percentheal 75,0;
				percentheal 0,75;
				close;
			}
			mes "[Chef]";
			mes "This meal costs 4,000 zeny.";
			mes "How many times do I have to tell you?";
			close;
		case 4:
			if (callfunc("F_HasEdenGroupMark")) {
				if (Zeny > 4499) {
					mes "[Chef]";
					mes "Hey, here you are.";
					mes "Enjoy your meal.";
					next;
					mes "- The Rib Eye Roll is grilled on the oak.";
					mes "- After eating the meal, You feel a little bit full.";
					mes "You've recovered your HP and SP. -";
					set Zeny, Zeny - 4500;
					percentheal 100,0;
					percentheal 0,100;
					close;
				}
				mes "[Chef]";
				mes "This meal costs 5,000 zeny but I will serve it for 4,500.";
				mes "Since you are a eden member you get a 10% discount so, it's 4,500 zeny.";
				close;
			}
			if (Zeny > 4999) {
				mes "[Chef]";
				mes "Hey, here you are.";
				mes "Enjoy your meal.";
				next;
				mes "Hey, here you are.";
				mes "- The Rib Eye Roll is grilled on the oak.";
				mes "- After eating the meal, You feel a little bit full.";
				mes "You've recovered your HP and SP. -";
				set Zeny, Zeny - 5000;
				percentheal 100,0;
				percentheal 0,100;
				close;
			}
			mes "[Chef]";
			mes "This meal costs 5,000 zeny.";
			mes "How many times do I have to tell you?";
			close;
		case 5:
			mes "[Chef]";
			mes "It's up to you.";
			close;
		}
	case 2:
		if (callfunc("F_HasEdenGroupMark")) {
			mes "[Chef]";
			mes "Most jobs should be managed by yourself. So it might be difficult, right?";
			mes "Actually it's harmful so they have requested continuously.";
			next;
			mes "[Chef]";
			mes "Can't we all work together by helping each other out?";
			mes "Anyway, you...";
			next;
			if (para_suv01 == 0) {
				if (BaseLevel < 41) {
					mes "[Chef]";
					mes "How are you?";
					mes "Ah... now you don't look like a beginner.";
					mes "Do you want to know some good information?";
					next;
					mes "[Chef]";
					mes "The Eden Group... sometimes receives jobs.";
					mes "But they also manufacture armor and weapons by themselves.";
					next;
					mes "[Chef]";
					mes "If you want, you can get a uniform from the Eden Group.";
					next;
					mes "[Chef]";
					mes "To get it you need to complete their missions.";
					mes "If you have any interest, meet Instructor Boya at the desk.";
					close;
				}
				mes "[Chef]";
				mes "You look like an expert...";
				mes "umm emm.. ";
				mes "umm emm.. ummm..";
				next;
				mes "[Chef]";
				mes "Yes! I found a thing that you are missing!";
				mes "You didn't get eden group uniform?";
				next;
				mes "[Chef]";
				mes "Hmm, it's not neccessary but if you remain with the eden group, it would be a good symbol.";
				next;
				mes "[Chef]";
				mes "You can't get it easily but..";
				mes "If you have an interest, ask Instructor Boya at the desk.";
				close;
			}
			mes "[Chef]";
			mes "Sure, the uniform goes well with you.";
			mes "Oh, sure.";
			mes "The leader makes the designs these days.";
			next;
			mes "[Chef]";
			mes "Ahah..especially the hat.";
			mes "To.. me.. the hat... um.";
			mes "It goes well with Laime but with Luke... I don't know..";
			next;
			switch (select("What about the hat?:Luke?")) {
			case 1:
				mes "[Chef]";
				mes "Nothing. It's so cute.";
				mes "I am concerned about the ribbon.";
				mes "But, it is essential that I wear this hat..";
				next;
				mes "[Chef]";
				mes "Hahahah I don't care.";
				mes "I don't care at all!";
				mes "Hahaha.. aaaahahahaha... ";
				close;
			case 2:
				mes "[Chef]";
				mes "He is a knife expert.";
				mes "One of our guard leaders.";
				mes "Who is also in charge of the security in this office.";
				next;
				mes "[Chef]";
				mes "Almost everyone just hired wonders about him.";
				mes "But Laime and Luke are totally different.";
				next;
				mes "[Chef]";
				mes "The leaders have worked together for a long time.";
				mes "But the leader wants to hide his past...";
				next;
				mes "[Chef]";
				mes "What I guess is... he might be a son of a rich family.";
				mes "Rumor is that Luke was the family guard.";
				mes "Laime was a servant? Wasn't she?";
				mes "What do you think about my story?";
				next;
				select(".....");
				mes "[Chef]";
				mes "............";
				mes "Ahah..hey~ even if I talked useless things don't look at me like that";
				mes "but it's really true that three people established this Eden Group.";
				close;
			}
		}
		mes "[Chef]";
		mes "Uh? Missions. Did you find the right place?";
		mes "Also let me se... you are not one of our members. How can I give you work?";
		next;
		mes "[Chef]";
		mes "Just eat in the restaurant.";
		mes "We serve meals to everyone.";
		mes "I am pretty sure they taste great!";
		close;
	}
}

moc_para01,112,86,4	script	[ Blacksmith ]#2nd10	2_M_OLDBLSMITH,{
	if (checkweight(2571,5) == 0) {
		mes "- You have too many items in your inventory to continue -";
		close;
	}
	mes "[Thorn]";
	if (!callfunc("F_HasEdenGroupMark")) {
		mes "Hmm, are you a member of Eden Group?";
		mes "You need to be one of our members to use my services.";
		close;
	}
	mes "What's going on?";
	next;
	switch( select( "Strengthen Equipment", "Personal Request", "Give me new equipment." ) ) {
		mes "[Thorn]";
	case 1:
		if (para_2nd01 < 6) {
			mes "Hmm equipment strengthening.";
			mes "I don't exactly do that for just anyone.";
			mes "The first step is at least a higher degree of training so have you done the Eden Group training yet?";
			next;
			mes "[Thorn]";
			mes "You have not awakened yet.";
			mes "First do the missions set forth by Instructor Ur and I'll help you strengthen your equipment.";
			next;
			mes "[Thorn]";
			mes "I won't help you unless you do that first!";
		}
		else if (para_2nd04 == 3) {
			mes "Strengthen your equipment eh?";
			mes "I'll strengthen the new Eden Group equipment for you by giving it a random stat boost.";
			next;
			mes "[Thorn]";
			mes "So what equipment do you want to enhance?";
			next;
			switch( select( "Eden Group Manteau II", "Eden Group Armor IV", "Eden Group Boots IV" ) ) {
				case 1: callsub S_Enchant,2571,"Manteau";// Para_Team_Manteau2
				case 2: callsub S_Enchant,15031,"Armor";// Para_Team_Armor
				case 3: callsub S_Enchant,2473,"Boots";// Para_Team_Boots4
			}
		}
		else {
			mes "I will strengthen your equipment after you've done a personal request of mine.";
			next;
			mes "[Thorn]";
			mes "I just want to be sure that you're worthy of using my services.";
		}
		close;
	case 2:
		if (para_2nd02 < 2) {
			mes "Have you started the request of Instructor Ur?";
			mes "I'm lending my services to those who are worthy.";
			next;
			mes "[Thorn]";
			mes "Besides, you won't have the equipment that I can enhance until you do those missions.";
		}
		else if (para_2nd04 == 0) {
			mes "Glad you're here.";
			mes "As a blacksmith I am in need of Iron materials constantly.";
			next;
			mes "[Thorn]";
			mes "Of course there will be a reward eventually...";
			mes "So...";
			next;
			mes "[Thorn]";
			if (BaseLevel < 80) {
				mes "Bring me some materials.";
				mes "I need 20 Iron Ore, and 10 Iron.";
			}
			else {
				mes "Bring me some materials.";
				mes "Bring 20 Used Iron Plate.";
				.@add = 1;
			}
			mes "It shouldn't be hard to get these can you bring them right away?";
			next;
			switch( select( "Sure thing.", "I don't want to." ) ) {
				mes "[Thorn]";
			case 1:
				mes "I appreciate it.";
				mes "So take your time and I'll be waiting.";
				para_2nd04 = 1 + .@add;
				setquest (7238 + .@add);// Errands for Thorn (easy/hard)
				close;
			case 2:
				mes "Eh...?";
				mes "Guess you're too busy to help me.";
				close;
			}
		}
		else if (para_2nd04 == 1 || para_2nd04 == 2) {
			if (para_2nd04 == 1) {
				setarray .@item[0],1002,998;// Iron_Ore, Iron
				setarray .@amount[0],20,10;
				setarray .@countitem[0],countitem(1002),countitem(998);
				.@string$ = "I asked you to get 20 Iron Ore, and 10 Iron.";
			}
			else {
				.@item[0] = 7319;
				.@amount[0] = 20;
				.@countitem[0] = countitem(7319);
				.@add = 1;
				.@string$ = "I asked you to get 20 Used Iron Plate.";
			}
			if (.@countitem[0] >= .@amount[0] && .@countitem[1] >= .@amount[1]) {
				mes "Let me guess...";
				mes "You've brought me the best quality materials.";
				mes "Good.";
				next;
				mes "[Thorn]";
				mes "I can always use more Iron materials.";
				mes "Instead of a normal reward I'll give you an advanced equipment.";
				next;
				mes "[Thorn]";
				mes "If at any time you think you have equipment that you need to strengthen come by me again.";
				delitem .@item[0],.@amount[0];
				if (.@countitem[1])
					delitem .@item[1],.@amount[1];
				para_2nd04 = 3;
				erasequest (7238 + .@add);
				setquest 7240;
			}
			else {
				mes .@string$;
				mes "I'm pretty sure that it isn't that difficult to do.";
			}
		}
		else if (para_2nd04 == 3) {
			mes "Now, that's it.";
			mes "That's enough for today.";
			mes "For now, ask me to strengthen your equipment and I'll do it for you.";
			next;
			mes "[Thorn]";
			mes "If you have one of these bring them to me.";
			mes "Eden Group Manteau II, Eden Group Armor IV, Eden Group Boots IV.";
		}
		else if (para_2nd04 == 4) {
			if (checkquest(7241,PLAYTIME) == 0) {
				mes "Come back later.";
				mes "You have to wait for some time first.";
			}
			else {
				mes "Uh-huh...";
				mes "What are we going to... oh don't worry.";
				mes "Come to me later.";
				erasequest 7241;// Errands for Thorn timer
				para_2nd04 = 0;
			}
		}
		close;
	case 3:
		if (para_2nd02 < 1) {
			mes "I'm not permitted to do this by Instructor Ur.";
			mes "Don't mess with me.";
		}
		else if (para_2nd02 == 1) {
			mes "Okay, I'll give you equipment.";
			mes "I'll give you good armor and equipment that you'll be proud of.";
			next;
			mes "[Thorn]";
			mes "So now, ^4d4dffmake sure that you have enough free inventory space.^000000";
			next;
			if (select( "Let me organize my inventory.", "I have free space." ) == 1) {
				mes "[Thorn]";
				mes "You do that.";
				mes "Free up your inventory so that you can get the weapons, boots, and all the other equipment.";
				close;
			}
			mes "[Thorn]";
			if (BaseJob == Job_Knight || BaseJob == Job_Crusader) {
				callsub L_Select,"One-handed sword, two-handed sword or spear.",
					1197,"Eden Group Slayer III","Two-handed Sword, Attack 200",
					13434,"Eden Group Saber III","One-handed Sword, Attack 185",
					1434,"Eden Group Spear I","One-handed, Attack 165";
			}
			else if (BaseJob == Job_Assassin) {
				callsub L_Select,"You have your choice of dagger or katar.",
					13066,"Eden Group Dagger III","Dagger, MATK +80, ATK 165",
					1289,"Eden Group Katar I","Katar, ATK 155";
			}
			else if (BaseJob == Job_Rogue || BaseClass == Job_Novice)
				getitem 13066,1;// P.Dagger III
			else if (BaseJob == Job_Alchemist || BaseJob == Job_Blacksmith) {
				callsub L_Select,"Your options are Mace, Saber and Axe.",
					16014,"Eden Group Mace III","Mace, Attack 172",// P_Mace3
					13434,"Eden Group Saber III","Sword, Attack 185",//P.Saber III
					1391,"Eden Group Axe I","Axe, Attack 195"; //P.Two-Handed Axe I
			}
			else if (BaseJob == Job_Wizard)
				getitem 1658,1;// P.Staff III
			else if (BaseJob == Job_Sage) {
				callsub L_Select,"",
					1658,"Eden Group Staff III","Staff, MATK + 170, INT +4, Attack 60", //P.Staff III
					1583,"Eden Group Dictionary I","Book, MATK +100, Attack 135"; //P.Book I
			}
			else if (BaseJob == Job_Priest) {
				callsub L_Select,"",
					1658,"Eden Group Staff III","Staff, MATK + 170, INT +4, Attack 60", //P.Staff III
					1583,"Eden Group Dictionary I","Book, MATK +100, Attack 135", //P.Book I
					16014,"Eden Group Mace III","Mace, Attack 172"; //P.Mace III
			}
			else if (BaseJob == Job_Monk) {
				callsub L_Select,"",
					1831,"Eden Group Knuckles I","Knuckle, Attack 120", //P.Knuckle I
					16014,"Eden Group Mace III","Mace, Attack 172"; //P.Mace III
			}
			else if (BaseJob == Job_Hunter)
				getitem 18106,1;// P.Bow III
			else if (BaseJob == Job_Bard) {
				callsub L_Select,"",
					18106,"Eden Group Bow III","Bow, Attack 140", //P.Bow III
					1931,"Eden Group Guitar I","Guitar, Attack 125"; //P.Guitar I
			}
			else if (BaseJob == Job_Dancer) {
				callsub L_Select,"",
					18106,"Eden Group Bow III","Bow, Attack 140", //P.Bow III
					1986,"Eden Group Whip I","Whip, Attack 125"; //P.Tail I
			}
			else if (BaseJob == Job_Ninja) {
				callsub L_Select,"",
					13066,"Eden Group Dagger III","Dagger, MATK +80, ATK 165", //P.Dagger III
					13310,"Eden Group Huuma Shuriken I","Huuma Suriken, MATK +50, ATK 170"; //P.Huuma Suriken I
			}
			else if (BaseJob == Job_Star_Gladiator)
				getitem 1583,1; //P.Book I
			else if (BaseJob == Job_Soul_Linker) {
				callsub L_Select,"",
					13066,"Eden Group Dagger III","Dagger, MATK +80, ATK 165", //P.Dagger III
					1658,"Eden Group Staff III","Staff, MATK +170, INT +4, ATK 60"; //P.Staff III
			}
			else if (BaseJob == Job_Gunslinger)
				getitem 13114,1; //P.Revolver III
			else if (BaseJob == Job_Summoner)
				getitem 26101,1; //P.Foxtail III
			else {
				mes "Hmm, you are eligible to receive equipment.";
				mes "Yeah I understand that.";
				next;
				mes "[Thorn]";
				mes "Hmm something went wrong though. I'm not sure what...";
				close;
			}
			para_2nd02 = 2;
			getitem 18514,1;// Paradise Hat II
			getitem 2571,1;// Paradise Mantle II
			getitem 2473,1;// Paradise Boots IV
			getitem 15031,1;// Paradise Uniform IV
			mes "Here's your equipment.";
			mes "I'll also give you the other gear that comes along with this set.";
		}
		else if (para_2nd02 == 2) {
			mes "You can strengthen the equipment that I gave you.";
			mes "Weapons Expert BK is at Eden Group Headquarters and he can do that for you.";
			next;
			mes "[Thorn]";
			mes "So go to him and he can make your equipment stronger.";
		}
		else {
			mes "Have you gotten your gear yet?";
			mes "I can give you the gear but I can't strengthen it for you.";
			next;
			mes "[Thorn]";
			mes "Your armor can be strengthened by someone else.";
			mes "I wish I could but I can't.";
		}
	}
	close;
L_Select:
	mes "Choose a weapon.";
	mes getarg(0);
	mes "Choose from the following.";
	for ( .@i = 1; .@i < getargcount(); .@i += 3 ) {
		set .@menu$, .@menu$ + getarg(.@i+1) + ":";
		mes getarg(.@i+1)+" : "+ getarg(.@i+2);
	}
	next;
	.@i = (select(.@menu$) -1) *3 +1;
	mes "[Toren]";
	getitem getarg(.@i),1;
	return;
S_Enchant:
	mes "[Thorn]";
	if (countitem(getarg(0)) > 0) {
		// .@1st = rand(1,12);
		// .@2nd = rand(1,12);

		// if (.@1st == 1) .@1st = 4751;// Luck2
		// else if (.@1st == 2) .@1st = 4787;// Mdef4
		// else if (.@1st == 3) .@1st = 4791;// Def3
		// else if (.@1st == 4) .@1st = 4701;// Strength2
		// else if (.@1st == 5) .@1st = 4788;// Mdef6
		// else if (.@1st == 6) .@1st = 4792;// Def6
		// else if (.@1st == 7) .@1st = 4731;// Agility2
		// else if (.@1st == 8) .@1st = 4789;// Mdef8
		// else if (.@1st == 9) .@1st = 4793;// Def9
		// else if (.@1st == 10) .@1st = 4721;// Dexterity2
		// else if (.@1st == 11) .@1st = 4711;// Inteligence2
		// else if (.@1st == 12) .@1st = 4741;// Vitality2

		// if (.@2nd == 1) .@2nd = 4701;// Strength2
		// else if (.@2nd == 2) .@2nd = 4787;// Mdef4
		// else if (.@2nd == 3) .@2nd = 4741;// Vitality2
		// else if (.@2nd == 4) .@2nd = 4791;// Def3
		// else if (.@2nd == 5) .@2nd = 4788;// Mdef6
		// else if (.@2nd == 6) .@2nd = 4792;// Def6
		// else if (.@2nd == 7) .@2nd = 4721;// Dexterity2
		// else if (.@2nd == 8) .@2nd = 4793;// Def9
		// else if (.@2nd == 9) .@2nd = 4751;// Luck2
		// else if (.@2nd == 10) .@2nd = 4789;// Mdef8
		// else if (.@2nd == 11) .@2nd = 4731;// Agility2
		// else if (.@2nd == 12) .@2nd = 4711;// Inteligence2
		setarray .@bonus[1],
			4751,// Luck2
			4787,// Mdef4
			4791,// Def3
			4701,// Strength2
			4788,// Mdef6
			4792,// Def6
			4731,// Agility2
			4789,// Mdef8
			4793,// Def9
			4721,// Dexterity2
			4711,// Inteligence2
			4741;// Vitality2
		.@1st = .@bonus[ rand(1,12) ];
		.@2nd = .@bonus[ rand(1,12) ];
		mes "Eden Group "+ getarg(1) +"?";
		mes "I understand.";
		mes "Remember the stats that are enhanced will be random.";
		mes "You sure you want me to enhance this?";
		next;
		switch( select( "I changed my mind.", "Yes, enhance it." ) ) {
			mes "[Thorn]";
		case 1:
			mes "You sure you don't want to enhance it?";
			mes "It's your choice.";
			close;
		case 2:
			mes "I understand.";
			mes "Let's begin the enhancement.";
			next;
			specialeffect EF_REPAIRWEAPON;
			progressbar "ffff00",3;
			delitem getarg(0),1;
			para_2nd04 = 4;
			// erasequest 7240;// Equipment Enhancement
			// setquest 7241;// Errands for Thorn timer
			changequest 7240,7241;
			getitem2 getarg(0),1,1,0,0,0,0,.@1st,.@2nd;						
			mes "[Thorn]";
			mes "Ok, seems that the enhancement went well.";
			mes "If you don't like the result then you can come back again tomorrow.";
			close;
		}
	}
	mes "Hmm... Don't you have the "+ getarg(1) +"?";
	mes "I don't think you have it with you.";
	close;
OnInit:
	setunitdata( getnpcid(0), UNPC_GROUP_ID,6 );
	setunittitle( getnpcid(0), "Thorn" );
	end;	
}

moc_para01,112,79,4	script	[ Weapons Expert ]#2nd11	4_M_REPAIR,{
	setarray .@wplist[0],
		1197,	// P_Slayer3
		13434,	// P_Saber3
		13066,	// P_Dagger3
		1289,	// P_Katar1
		1391,	// P_Two_Handed_Axe1
		1434,	// P_Spear I
		1658,	// P_Staff3
		16014,	// P_Mace3
		18106,	// P_Bow3
		1583,	// P.Book I
		1931,	// P_Guitar I
		1986,	// P_Tail1
		13114,	// P_Revolver3
		1831,	// P_Knuckle1
		13310,	// P_Huuma_Shuriken1
		26101;	// P_Foxtail III
	.@wplist_size = getarraysize(.@wplist);

	.@menu$ = "Eden Group Slayer III:" +
		"Eden Group Saber III:" +
		"Eden Group Dagger III:" +
		"Eden Group Katar I:" +
		"Eden Group Axe I:" +
		"Eden Group Spear I:" +
		"Eden Group Staff III:" +
		"Eden Group Mace III:" +
		"Eden Group Bow III:" +
		"Eden Group Dictionary I:" +
		"Eden Group Guitar I:" +
		"Eden Group Whip I:" +
		"Eden Group Revolver III:" +
		"Eden Group Knuckles I:" +
		"Eden Group Huuma Shuriken I:" +
		"Eden Group Foxtail III";
	mes "[BK]";
	mes "What's happening?";
	mes "No way?!";
	next;
	if (select( "Enhancing weapons.", "What?" ) == 2) {
		mes "[BK]";
		mes "Yeah?";
		mes "Are you asking me what I know?";
		next;
		select("...??");
		mes "[BK]";
		mes ".....????";
		close;
	}
	mes "[BK]";
	mes "Ah, you're here to enhance your gear.";
	mes "Okay, shall we?";
	next;
	mes "[BK]";
	if (para_2nd02 < 2) {
		mes "You have to have the equipment first in order for me to do anything.";
		mes "Didn't you get anything from Thorn yet?";
	}
	else if (para_2nd02 == 2) {
		if (para_2nd01 < 11) {
			mes "Hmm, no.";
			mes "I don't have permission from Instructor Ur.";
			mes "You have to finish your training.";
			next;
			mes "[BK]";
			mes "Once you do that I can help you.";
			mes "Until then I won't talk to you.";
		}
		else {
			mes "Okay, what weapon do you want to enhance?";
			next;
			.@s = select(.@menu$) -1;
			mes "[BK]";
			if (countitem(.@wplist[.@s]) > 0) {
				mes "Ok for the first reinforcement.";
				mes "ATK + 3% or MATK + 3%?";
				mes "I'll give you the option to grant this effect on the selected gear.";
				next;
				mes "[BK]";
				mes "You can't change your mind after so choose wisely.";
				next;
				if (select( "ATK +3%","MATK +3%" ) == 1) {
					.@string$ = "Ok, you've chosen to raise physical attack.";
					.@card = 4767;
				}
				else {
					.@string$ = "Ok, you've chosen to raise magical attack.";
					.@card = 4806;
					.@add = 1;
				}
				mes "[BK]";
				mes .@string$;
				mes "Would you look at that. Your weapon will now be stronger.";
				para_2nd03 = 1 + .@add;
				next;
				specialeffect EF_REPAIRWEAPON;
				progressbar "ffff00",3;
				para_2nd02 = 3;
				delitem .@wplist[.@s],1;
				getitem2 .@wplist[.@s],1,1,0,0,0,0,0,.@card;
				mes "[BK]";
				mes "Isn't it magnificent?";
				mes "I know, I know, I'm pretty amazing!";
			}
			else {
				mes "Are you kidding me?";
				mes "You don't seem to have that weapon in your possession.";
			}
		}
	}
	else if (para_2nd02 == 3) {
		if (para_2nd01 < 16) {
			mes "Hmm, I'm not sure you're ready.";
			mes "All 3 steps of Instructor Ur's missions have to be completed first.";
			next;
			mes "[BK]";
			mes "You get what I'm saying?";
			mes "Finish the third mission from Instructor Ur.";
		}
		else {
			mes "Good. So you previously received an addition to Atk or Matk.";
			mes "For this option you can choose from Brute, Plant, Fish, Dragon, Increased Heal.";
			next;
			mes "[BK]";
			mes "Once you've chosen you can't change your mind again.";
			mes "But your weapon will increase damage to the type of enhancement you pick.";
			next;
			mes "[BK]";
			mes "Basically I will add a random monster card with the property type that you choose.";
			mes "Good luck!";
			next;
			switch( select( "Brute Type", "Plant Type", "Insect Type", "Fish Type", "Dragon Type", "Increased Heal" ) ) {
			case 1:
				setarray .@arg[0],4060,4472,111;// Goblin_Card, Bradium_Goram_Card
				.@type$ = "Brute";
				break;
			case 2:
				setarray .@arg[0],4068,4470,121;// Scorpion_Card, Nepenthes_Card
				.@type$ = "Plant";
				break;
			case 3:
				setarray .@arg[0],4063,4476,131;// Caramel_Card, Pinguicula_Card
				.@type$ = "Insect";
				break;
			case 4:
				setarray .@arg[0],4080,4469,141;// Flora_Card, Naga_Card
				.@type$ = "Fish";
				break;
			case 5:
				setarray .@arg[0],4118,4471,151;// Petit_Card, Egg_Of_Draco_Card
				.@type$ = "Dragon";
				break;
			case 6:
				setarray .@arg[0],4805,4805,161;// Heal_Amount2
			}
			mes "[BK]";
			for ( .@i = 0; .@i < .@wplist_size; .@i++ ) {
				if (countitem(.@wplist[.@i]) > 0) {
					if (.@arg[0] == 4805)
						mes "Good. Your weapon will now increase Heal effectiveness.";
					else
						mes "Good. Your weapon will increase attacks to "+ .@type$ +" type monsters.";
					next;
					specialeffect EF_REPAIRWEAPON;
					progressbar "ffff00",3;
					delitem .@wplist[.@i],1;
					para_2nd02 = 4;
					if (para_2nd03 == 1) {
						para_2nd03 = .@arg[2];
						getitem2 .@wplist[.@i],1,1,0,0,0,0,.@arg[0],4767;
					}
					else {
						para_2nd03 = .@arg[2] + 100;
						getitem2 .@wplist[.@i],1,1,0,0,0,0,.@arg[1],4806;
					}
					mes "[BK]";
					mes "Isn't it magnificent?";
					mes "I know, I know, I'm pretty amazing!";
					close;
				}
			}
			mes "Sorry, but it doesn't seem like you have a weapon that can be enhanced.";
			mes "How did this happen?";
			close;
		}
	}
	else if (para_2nd02 == 4) {
		if (para_2nd01 < 30) {
			mes "Instructor Ur has a lot of training missions doesn't he?";
			mes "There's only one more stage.";
			mes "Reach the final stage of training.";
		}
		else {
			mes "Okay, this is the last step.";
			mes "I will make your weapon even more enhanced.";
			next;
			mes "[BK]";
			mes "The card added from the previous step will be doubled.";
			mes "And if you have a healing weapon it will increase it's healing efficiency.";
			mes "You understand?";
			next;
			mes "[BK]";
			mes "Ok what weapon did you have now?";
			next;
			.@s = select(.@menu$) -1;
			mes "[BK]";
			if (countitem(.@wplist[.@s]) > 0) {
				mes "Doubling the strength of your bonus stat.";
				next;
				specialeffect EF_REPAIRWEAPON;
				progressbar "ffff00",3;
				delitem .@wplist[.@s],1;
				para_2nd02 = 5;
				if (para_2nd03 == 111)
					getitem2 .@wplist[.@s],1,1,0,0,0,4060,4060,4767;// Goblin_Card, Goblin_Card, Atk3
				else if (para_2nd03 == 121)
					getitem2 .@wplist[.@s],1,1,0,0,0,4068,4068,4767;// Scorpion_Card, Scorpion_Card, Atk3
				else if (para_2nd03 == 131)
					getitem2 .@wplist[.@s],1,1,0,0,0,4063,4063,4767;// Caramel_Card, Caramel_Card, Atk3
				else if (para_2nd03 == 141)
					getitem2 .@wplist[.@s],1,1,0,0,0,4080,4080,4767;// Flora_Card, Flora_Card, Atk3
				else if (para_2nd03 == 151)
					getitem2 .@wplist[.@s],1,1,0,0,0,4118,4118,4767;// Petit_Card, Petit_Card, Atk3
				else if (para_2nd03 == 161)
					getitem2 .@wplist[.@s],1,1,0,0,0,4805,4805,4767;// Heal_Amount2, Heal_Amount2, Atk3
				else if (para_2nd03 == 211)
					getitem2 .@wplist[.@s],1,1,0,0,0,4472,4472,4806;// Bradium_Goram_Card, Bradium_Goram_Card, Matk3
				else if (para_2nd03 == 221)
					getitem2 .@wplist[.@s],1,1,0,0,0,4470,4470,4806;// Nepenthes_Card, Nepenthes_Card, Matk3
				else if (para_2nd03 == 231)
					getitem2 .@wplist[.@s],1,1,0,0,0,4476,4476,4806;// Pinguicula_Card, Pinguicula_Card, Matk3
				else if (para_2nd03 == 241)
					getitem2 .@wplist[.@s],1,1,0,0,0,4469,4469,4806;// Naga_Card, Naga_Card, Matk3
				else if (para_2nd03 == 251)
					getitem2 .@wplist[.@s],1,1,0,0,0,4471,4471,4806;// Egg_Of_Draco_Card, Egg_Of_Draco_Card, Matk3
				else if (para_2nd03 == 261)
					getitem2 .@wplist[.@s],1,1,0,0,0,4805,4805,4806;// Heal_Amount2, Heal_Amount2, Matk3
				mes "[BK]";
				mes "Isn't it magnificent?";
				mes "I know, I know, I'm pretty amazing!";
			}
			else {
				mes "Hey, you think I'm joking?";
				mes "Do you have the weapons or not?";
			}
		}
	}
	else if (para_2nd02 > 4) {
		mes "How do you like your enhanced weapon?";
		mes "Hopefully it has helped you.";
		next;
		mes "[BK]";
		mes "I've given you the peak of your weapon's efficiency.";
		mes "Weapons aren't the only things that need strength, though, don't you agree?";
		next;
		mes "[BK]";
		mes "Come on admit it.";
		mes "Thorn gave inspiration and I intend on providing strong equipment, that's how to live life.";
		mes "Hahahaha!";
	}
	close;
OnInit:
	setunitdata( getnpcid(0), UNPC_GROUP_ID,6 );
	setunittitle( getnpcid(0), "BK" );
	end;	
}

sec_in02,25,33,4	script	Assistant#para_suvquest	422,{
	callfunc "F_GM_NPC";
	mes "Password";
	next;
	if (callfunc("F_GM_NPC",1854,0) == 1) {
		mes "Please select the variable you want to modify.";
		next;
		set .@var, select("para_suv01:para_suv02");
		mes "Enter the modified value";
		next;
		input .@input,0,9999;
		mes "Value of para_suv0"+.@var+" has been changed to "+.@input+".";
		setd "para_suv0"+.@var,.@input;
		close;
	} else {
		mes "......meow wee.";
		close;
	}
}
