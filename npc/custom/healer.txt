//===== rAthena Script =======================================
//= Healer
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Basic healer script.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.1 Aligned coordinates with @go.
//============================================================

-	script	Healer	-1,{

	.@Price = 1000;	// Zeny required for heal
	.@Buffs = 1;	// Also buff players? (1: yes / 0: no)
	.@Delay = 5;	// Heal delay, in seconds

	if (@HD > gettimetick(2))
		end;
	if (.@Price) {
		message strcharinfo(0), "Healing costs " + callfunc("F_InsertComma",.@Price) + " Zeny.";
		if (Zeny < .@Price)
			end;
		if (select("^0055FFHeal^000000:^777777Cancel^000000") == 2)
			end;
		Zeny -= .@Price;
	}
	specialeffect2 EF_HEAL2;
	percentheal 100,100;
	if (.@Buffs) {
		specialeffect2 EF_INCAGILITY;
		sc_start SC_INCREASEAGI,240000,10;
		specialeffect2 EF_BLESSING;
		sc_start SC_BLESSING,240000,10;
	}
	if (.@Delay)
		@HD = gettimetick(2) + .@Delay;
	end;

OnInit:
	setunitdata (getnpcid(0),UNPC_SEX,SEX_FEMALE);
	setunitdata (getnpcid(0),UNPC_CLASS,JOB_HIGH_PRIEST);
	setunitdata (getnpcid(0),UNPC_HAIRSTYLE,4);
	setunitdata (getnpcid(0),UNPC_HAIRCOLOR,2);
	setunitdata (getnpcid(0),UNPC_HEADBOTTOM,1881);
	setunitdata (getnpcid(0),UNPC_HEADMIDDLE,1945);
	setunitdata (getnpcid(0),UNPC_HEADTOP,804);
	setunitdata (getnpcid(0),UNPC_GROUP_ID,39);
	setunittitle (getnpcid(0),"Sister Frieda");
}


// Duplicates
//============================================================
alberta,25,240,6	duplicate(Healer)	Healer#alb	HIDDEN_NPC
aldebaran,135,118,6	duplicate(Healer)	Healer#alde	HIDDEN_NPC
amatsu,200,79,4	duplicate(Healer)	Healer#ama	HIDDEN_NPC
ayothaya,207,169,6	duplicate(Healer)	Healer#ayo	HIDDEN_NPC
comodo,184,158,6	duplicate(Healer)	Healer#com	HIDDEN_NPC
einbech,57,36,6	duplicate(Healer)	Healer#einbe	HIDDEN_NPC
einbroch,57,202,6	duplicate(Healer)	Healer#einbr	HIDDEN_NPC
geffen,115,72,6	duplicate(Healer)	Healer#gef	HIDDEN_NPC
gonryun,156,122,6	duplicate(Healer)	Healer#gon	HIDDEN_NPC
hugel,89,150,6	duplicate(Healer)	Healer#hug	HIDDEN_NPC
izlude,121,150,6	duplicate(Healer)	Healer#izl	HIDDEN_NPC	//Pre-RE: (125,118)
jawaii,250,139,4	duplicate(Healer)	Healer#jaw	HIDDEN_NPC
lighthalzen,152,100,6	duplicate(Healer)	Healer#lhz	HIDDEN_NPC
louyang,226,103,4	duplicate(Healer)	Healer#lou	HIDDEN_NPC
manuk,272,144,6	duplicate(Healer)	Healer#man	HIDDEN_NPC
mid_camp,203,289,6	duplicate(Healer)	Healer#mid	HIDDEN_NPC
moc_ruins,72,164,4	duplicate(Healer)	Healer#moc	HIDDEN_NPC
morocc,154,97,4	duplicate(Healer)	Healer#mor	HIDDEN_NPC
moscovia,220,191,4	duplicate(Healer)	Healer#mos	HIDDEN_NPC
niflheim,212,182,5	duplicate(Healer)	Healer#nif	HIDDEN_NPC
payon,179,106,4	duplicate(Healer)	Healer#pay	HIDDEN_NPC
prontera,162,193,4	duplicate(Healer)	Healer#prt	HIDDEN_NPC
rachel,125,116,6	duplicate(Healer)	Healer#rac	HIDDEN_NPC
splendide,201,153,4	duplicate(Healer)	Healer#spl	HIDDEN_NPC
thor_camp,249,74,4	duplicate(Healer)	Healer#thor	HIDDEN_NPC
umbala,105,148,3	duplicate(Healer)	Healer#umb	HIDDEN_NPC
veins,217,121,4	duplicate(Healer)	Healer#ve	HIDDEN_NPC
xmas,143,136,4	duplicate(Healer)	Healer#xmas	HIDDEN_NPC
yuno,164,45,4	duplicate(Healer)	Healer#yuno	HIDDEN_NPC

// Duplicates (Renewal)
//============================================================
brasilis,194,221,6	duplicate(Healer)	Healer#bra	HIDDEN_NPC
dewata,195,187,4	duplicate(Healer)	Healer#dew	HIDDEN_NPC
dicastes01,201,194,4	duplicate(Healer)	Healer#dic	HIDDEN_NPC
ecl_in01,45,60,4	duplicate(Healer)	Healer#ecl	HIDDEN_NPC
malangdo,132,114,6	duplicate(Healer)	Healer#mal	HIDDEN_NPC
malaya,227,204,6	duplicate(Healer)	Healer#ma	HIDDEN_NPC
mora,55,152,4	duplicate(Healer)	Healer#mora	HIDDEN_NPC
