//===== rAthena Script ======================================= 
//= Novice Training Grounds
//===== By: ================================================== 
//= Dr.Evil & MasterOfMuppets
//= Updated by Louis T Steinhil
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena Project; SVN 3422+
//===== Description: ========================================= 
//= [Official Conversion]
//= Novice Training Grounds
//===== Additional Comments: ================================= 
//= 1.0 Initial Version
//= 1.1 Separated and improved Thief Guide [Louis T Steinhil]
//============================================================
-	script	Thief Guide#newbe10::NvThief	HIDDEN_NPC,{
	mes "";
	setdialogsize(400, 300);
	setdialogpospercent(50, 50);
	if (job_novice_q < 15) {
		mes "<B>[ Thief Guild Member ]</B>";
		mes "You look like you are not done with Instructor Brade's lessons.";
		close;
	}
	mes "<B>[ Thief Guild Member ]</B>";
	mes "How can I help you?";
	next;
	switch(select("About the Thief Class.:I want to be a Thief.:Got any Quests?:Cancel.")) {
	case 1:
		mes "<B>[ Thief Guild Member ]</B>";
		mes "Thieves are experts at using Dagger class weapons.";
		mes "They strike quickly and easily evade attacks from their enemies.";
		next;
		mes "<B>[ Thief Guild Member ]</B>";
		mes "Thieves can learn skills that allow them to hide from their enemies or steal items from monsters.";
		mes "They are also feared for their use of poison, which slowly weakens their enemies.";
		next;
		mes "<B>[ Thief Guild Member ]</B>";
		mes "In jeopardy, or in the case that thieves do not want to reveal themselves,";
		mes "they can even use their skill to hide themselves easily.";
		next;
		mes "<B>[ Thief Guild Member ]</B>";
		mes "Thieves can change their jobs to ^8E2323Assassins^000000 or ^8E2323Rogues^000000.";
		next;
		if (isequipped(2820) || countitem(2820)) {
			mes "<B>[ Thief Guild Member ]</B>";
			mes "The class manual can help you understand what it is to be a Thief by using the skills that are essential to all Thieves.";
			close;
		}
		mes "<B>[ Thief Guild Member ]</B>";
		mes "If you want, I'll give you a Thief skill manual.";
		mes "Would you like to experience the skills of a Thief?";
		next;
		switch(select("Yes, I would.:No, I don't.")) {
		case 1:
			mes "<B>[ Thief Guild Member ]</B>";
			mes "Here, please take the Thief manual.";
			mes "I'll just take the other class manuals while I'm at it.";
			next;
			callfunc "F_NvErase";
			getitem 2820,1; //Thief_Manual
			next;
			mes "<B>[ Thief Guild Member ]</B>";
			mes "Make sure to equip the Skill Manual by double-clicking it in the Inventory Window.";
			mes "Open your Skill Tree by pressing ^4d4dffAlt + S^000000 and minimize the window by pressing the ^4d4dff'-'^000000 button on the top right corner of the window to see the available skills.";
			close;
		case 2:
			mes "<B>[ Thief Guild Member ]</B>";
			mes "Oh, are you sure?";
			mes "Class selection is very important so please consider it carefully.";
			mes "If you want to ask about Thieves, please talk to me any time.";
			close;
		}
	case 2:
		mes "<B>[ Thief Guild Member ]</B>";
		mes "Do you really want to be a Thief?";
		mes "If so, I will send you to the Thief Guild immediately.";
		next;
		mes "<B>[ Thief Guild Member ]</B>";
		mes "You already reached the required job level, didn't you?";
		mes "Do you want to finish the Novice training and go to the Thief Guild?";
		next;
		switch(select("Let me consider it again.:Yes, I do.")) {
		case 1:
			mes "<B>[ Thief Guild Member ]</B>";
			mes "Have you tried all of the class manuals yet?";
			mes "If you're undecided what job to take on I suggest you try talking to all the Guides first.";
			close;
		case 2:
			mes "<B>[ Thief Guild Member ]</B>";
			mes "Ok, I see.";
			mes "Welcome. Then your Novice training is totally complete and you will be sent to the Thief Guild immediately.";
			callfunc "F_NvErase",1;
			close2;
			savepoint "morocc",150,99,1,1;
			warp "moc_prydb1",99,185;
			end;
		}
	case 3:
		set .@quest, checkquest(7127);
		if (.@quest == -1) {
			mes "<B>[ Thief Guild Member ]</B>";
			mes "Quests?";
			mes "Hmm, what will be good for you?";
			mes "How about hunting some Willows.";
			next;
			mes "<B>[ Thief Guild Member ]</B>";
			mes "You can't cheat this so make sure to kill 5 Willows. When you're done come and talk to me again.";
			mes "If you talk to Trainer Brutus, he can send you to a more difficult training ground where there are Willows to hunt.";
			setquest 7127;
			next;
			mes "<B>[ Thief Guild Member ]</B>";
			mes "If you equip the Thief manual, you can have the effect of the skill, Double Attack.";
			mes "Double Attack and Hide are the main skills of the Thief.";
			close;
		} else if (.@quest == 0 || .@quest == 1) {
			if (checkquest(7127,HUNTING) == 2) {
				mes "<B>[ Thief Guild Member ]</B>";
				mes "You are very strong.";
				mes "These are not many but let me give you a reward for your effort.";
				completequest 7127;
				getitem 12323,50; //N_Fly_Wing
				getexp 5000,100;
				close;
			} else {
				mes "<B>[ Thief Guild Member ]</B>";
				mes "Hunt 5 Willows and then come talk to me to finish your quest.";
				close;
			}
		} else if (.@quest == 2) {
			mes "<B>[ Thief Guild Member ]</B>";
			mes "I think you finished that quest already, right?";
			mes "You'll experience more of these quests as you grow as an adventurer in the world.";
			next;
			mes "<B>[ Thief Guild Member ]</B>";
			mes "Your training is now complete.";
			mes "If you choose to become a Thief I wish you good luck.";
			close;
		} else {
			mes "<B>[ Thief Guild Member ]</B>";
			mes "I'm sorry but I don't have any quests to give you right now.";
			close;
		}
	case 4:
		mes "<B>[ Thief Guild Member ]</B>";
		mes "If you have something to ask about the Thief job, feel free to ask me.";
		close;
	}
}
tg_1-1,147,228,5	duplicate(NvThief)	Thief Guide#nv1	2_M_THIEFMASTER
tg_1-2,147,228,5	duplicate(NvThief)	Thief Guide#nv2	2_M_THIEFMASTER
tg_1-3,147,228,5	duplicate(NvThief)	Thief Guide#nv3	2_M_THIEFMASTER
tg_1-4,147,228,5	duplicate(NvThief)	Thief Guide#nv4	2_M_THIEFMASTER
