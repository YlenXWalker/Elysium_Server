//===== rAthena Script ======================================= 
//= Novice Training Grounds
//===== By: ================================================== 
//= Dr.Evil & MasterOfMuppets
//= Updated by Louis T Steinhil
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena Project; SVN 3422+
//===== Description: ========================================= 
//= [Official Conversion]
//= Novice Training Grounds
//===== Additional Comments: ================================= 
//= 1.0 Initial Version
//= 2.5 Separated and improved Merchant Guide [Louis T Steinhil]
//============================================================
-	script	Merchant Guide#newbe10::NvMerchant	HIDDEN_NPC,{
	mes "";
	setdialogsize(400, 300);
	setdialogpospercent(50, 50);
	if (job_novice_q < 15) {
		mes "<B>[ Merchant Guildsman ]</B>";
		mes "You are still in the process of training with sir Brad.";
		close;
	}
	mes "<B>[ Merchant Guildsman ]</B>";
	mes "What can I help you with?";
	next;
	switch(select("Tell me about Merchants:I want to be a Merchant.:Give me a Quest.:Cancel")) {
	case 1:
		mes "<B>[ Merchant Guildsman ]</B>";
		mes "Merchant? Simply put, he sells goods and makes money.";
		mes "That's the main focus for any Merchant.";
		next;
		mes "<B>[ Merchant Guildsman ]</B>";
		mes "We have special skills for making money.";
		mes "We can buy goods at a lower price";
		mes "and sell them at a higher price.";
		next;
		mes "<B>[ Merchant Guildsman ]</B>";
		mes "Also Merchants can have Carts";
		mes "that we can store lots of items in.";
		next;
		mes "<B>[ Merchant Guildsman ]</B>";
		mes "^8E2323Merchants can transform to a Blacksmith";
		mes "^8E2323or an Alchemist^000000 as their second job.";
		next;
		if (isequipped(2823) || countitem(2823)) {
			mes "<B>[ Merchant Guildsman ]</B>";
			mes "I think you should test Merchant skills before you decide what to do.";
			close;
		}
		mes "<B>[ Merchant Guildsman ]</B>";
		mes "Do you want to see what it's like to be a Merchant?";
		next;
		switch(select("I want to test Merchant skills.:I don't want to test it.")) {
		case 1:
			mes "<B>[ Merchant Guildsman ]</B>";
			mes "Here is the Merchant skill manual.";
			mes "I will take other job manuals from you while I'm at it.";
			next;
			callfunc "F_NvErase";
			getitem 2823,1; //Merchant_Manual
			next;
			mes "<B>[ Merchant Guildsman ]</B>";
			mes "Make sure to equip the Skill Manual by double-clicking it in the Inventory Window.";
			mes "Open your Skill Tree by pressing ^4d4dffAlt + S^000000 and minimize the window by pressing the ^4d4dff'-'^000000 button on the top right corner of the window to see the available skills.";
			close;
		case 2:
			mes "<B>[ Merchant Guildsman ]</B>";
			mes "Hum. Do you think so.";
			mes "Ok, I agree with you. Choosing job is very important in our life.";
			mes "Just talk to me whenever you want to experience Merchant skills.";
			close;
		}
	case 2:
		mes "<B>[ Merchant Guildsman ]</B>";
		mes "You want to be a Merchant?";
		mes "Ok, then, I will send you to the Merchant Guild's union right away.";
		next;
		mes "<B>[ Merchant Guildsman ]</B>";
		mes "Wait, have you checked your Job level?";
		mes "Do you want to complete your Novice training and go straight to the Merchant Guild's union?";
		next;
		switch(select("I will think about it again.:Yes, I want to go to the Merchant Guild's union")) {
		case 1:
			mes "<B>[ Merchant Guildsman ]</B>";
			mes "Ok, I hope you consider being a Merchant.";
			mes "We are always welcome to have you!";
			close;
		case 2:
			mes "<B>[ Merchant Guildsman ]</B>";
			mes "That's great.";
			mes "Then I will end the Training Process and send you to the Merchant Guild's union immediately.";
			callfunc "F_NvErase",1;
			close2;
			savepoint "alberta",30,232,1,1;
			warp "alberta_in",62,44;
			end;
		}
	case 3:
		set .@quest, checkquest(7126);
		if (.@quest == -1) {
			mes "<B>[ Merchant Guildsman ]</B>";
			mes "Huh? Quest??";
			mes "Humm... What would be suited for you...?";
			mes "Ok, then bring me ^5d5dff300 zeny^000000 by selling some of your items to the Potato Merchant.";
			next;
			mes "<B>[ Merchant Guildsman ]</B>";
			mes "If you equipped the Merchant manual you have the ability to use the 'Overcharge' and 'Discount' skills.";
			setquest 7126;
			next;
			mes "<B>[ Merchant Guildsman ]</B>";
			mes "I will wait for you here.";
			mes "Good luck!";
			close;
		} else if (.@quest == 0 || .@quest == 1) {
			if (Zeny >= 300) {
				mes "<B>[ Merchant Guildsman ]</B>";
				mes "Excellent!";
				mes "Zeny is the currency of Midgard.";
				mes "You can make zeny by selling loot as well as through making smart business decisions.";
				next;
				mes "<B>[ Merchant Guildsman ]</B>";
				mes "We can use extremely powerful skills with zeny!";
				mes "Mammonite! That is our ultimate attack skill!!";
				mes "We basically paste money on the weapon and then strike the enemy. Death by Zeny! Nyahahahaha!";
				next;
				mes "<B>[ Merchant Guildsman ]</B>";
				mes "By the way, you have done an excellent job. So, I should give you something... What would be good?";
				mes "Alright! What about Phracon?";
				mes "Phracon is a metallic element which you can use for refining weapons.";
				completequest 7126;
				getitem 1010,7; //Phracon
				getexp 5000,100;
				close;
			} else {
				mes "<B>[ Merchant Guildsman ]</B>";
				mes "Zeny is the currency of Midgard.";
				mes "You can make zeny by selling loot as well as through making smart business decisions.";
				next;
				mes "<B>[ Merchant Guildsman ]</B>";
				mes "You can get loot by killing monsters. After hunting monsters, you can sell those loot to make a profit!";
				mes "What a valuable job it is! Isn't it?";
				next;
				mes "<B>[ Merchant Guildsman ]</B>";
				mes "Why don't you try to be a Merchant? Making money is not easy but it will be a great experience!";
				close;
			}
		} else if (.@quest == 2) {
			mes "<B>[ Merchant Guildsman ]</B>";
			mes "You have already completed the quest.";
			mes "Once you enter the main world, you can get various exciting quests there!";
			next;
			mes "<B>[ Merchant Guildsman ]</B>";
			mes "I think you're strong enough now to change your job right? Hahahaha don't waste too much time here Novice.";
			close;
		} else {
			mes "<B>[ Merchant Guildsman ]</B>";
			mes "I'm sorry but I don't have any quests to give you right now.";
			close;
		}
	case 4:
		mes "<B>[ Merchant Guildsman ]</B>";
		mes "If you have any questions about Merchants, feel free to ask me.";
		mes "I think you could be a rich Merchant. AhHahaha.";
		close;
	}
}
tg_1-1,151,232,3	duplicate(NvMerchant)	Merchant Guide#nv1	4W_M_01
tg_1-2,151,232,3	duplicate(NvMerchant)	Merchant Guide#nv2	4W_M_01
tg_1-3,151,232,3	duplicate(NvMerchant)	Merchant Guide#nv3	4W_M_01
tg_1-4,151,232,3	duplicate(NvMerchant)	Merchant Guide#nv4	4W_M_01
