//===== rAthena Script ======================================= 
//= Novice Training Grounds
//===== By: ================================================== 
//= Dr.Evil & MasterOfMuppets
//= Updated by Louis T Steinhil
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena Project; SVN 3422+
//===== Description: ========================================= 
//= [Official Conversion]
//= Novice Training Grounds
//===== Additional Comments: ================================= 
//= 1.0 Initial Version
//= 2.5 Separated and improved Kafra [Louis T Steinhil]
//============================================================
-	script	Kafra Employee#newbe07::NvKafra	HIDDEN_NPC,{
	mes "";
	setdialogsize(400, 300);
	setdialogpospercent(50, 50);
	if (job_novice_q < 8) {
		mes "<B>[ Kafra Employee ]</B>";
		mes "Hello.";
		mes "I don't think it's my turn yet.";
		mes "Please talk to 'Chocolat' first.";
		close;
	} else if (job_novice_q == 8) {
		mes "<B>[ Kafra Employee ]</B>";
		mes "Hello.";
		mes "I've been dispatched by the Kafra Head Office to help trainees.";
		mes "The Kafra Corporation is the world's largest company with a long and distinguished history on the Midgard continent.";
		mes "It's important for you adventurers to be close with us.";
		next;
		mes "<B>[ Kafra Employee ]</B>";
		mes "I will inform you about the Kafra Services which you will be using most frequently, [Save], [Storage] and [Teleport] Services.";
		next;
		mes "<B>[ Kafra Employee ]</B>";
		mes "When you talk to a Kafra Employee and ask for the Save Service,";
		mes "the location of where you will revive, after being defeated in battle, will be changed.";
		next;
		mes "<B>[ Kafra Employee ]</B>";
		mes "Your Respawn Point is always the last place where you have saved. Using a Butterfly Wing will return you to the place where you last saved.";
		mes "This service is free to use, so feel free to use the Save Service anytime you like.";
		next;
		callsub L_Services,2; //Storage
		next;
		callsub L_Services,3; //Teleport
		set job_novice_q,9;
		next;
		mes "<B>[ Kafra Employee ]</B>";
		mes "Do you want to hear another explanation?";
		next;
		while(1) {
			set .@select, select("About Save:About Storage:About Teleport:About Cart:No More.");
			if (.@select == 5) {
				mes "<B>[ Kafra Employee ]</B>";
				mes "I hope you are satisfied with my explanation";
				mes "about the Kafra Service.";
				close;
			}
			callsub L_Services, .@select;
			next;
		}
	} else {
		mes "<B>[ Kafra Employee ]</B>";
		mes "Kafra Service at your service.";
		mes "What can I do for you?";
		next;
		mes "<B>[ Kafra Employee ]</B>";
		mes "Let me inform you before you use the Teleport Service.";
		mes "If you teleport to the village,";
		mes "^4d4dffyou can NEVER come back to the Novice Training Field.^000000";
		mes "So keep that in mind.";
		next;
		switch(select("Teleport Service:About Kafra Service")) {
		case 1:
			mes "<B>[ Kafra Employee ]</B>";
			mes "So, you are going straight to town.";
			mes "Let me give you a brief introduction.";
			mes "^4d4dffProntera is the Capital City of the Rune-Midgarts Kingdom, and right next to it is Izlude^000000.";
			next;
			mes "<B>[ Kafra Employee ]</B>";
			mes "^4d4dffMorocc is the Desert City, you can change your job to Thief and Assassin^000000 here.";
			mes "^4d4dffPayon is the Village in the Mountains, and it is close to the Archer Village^000000.";
			mes "^4d4dffGeffen is the City of Magic. Go there if you want to become a Mage.^000000";
			next;
			mes "<B>[ Kafra Employee ]</B>";
			mes "^4d4dffAlberta is the Port City and there you can find the Merchant Guild^000000.";
			mes "Also, if you are trying to take a boat to go somewhere, you have to go through Alberta first.";
			mes "Select the town you want to teleport to.";
			next;
			switch(select("Prontera:Morocc:Payon:Alberta:Geffen")) {
			case 1:
				mes "<B>[ Kafra Employee ]</B>";
				mes "Rune-Midgarts' Capital, Prontera.";
				mes "I will send you to Prontera.";
				mes "May Freya bless you upon your journey.";
				callfunc "F_NvErase";
				close2;
				savepoint "prontera",117,72,1,1;
				warp "prontera",150,50;
				end;
			case 2:
				mes "<B>[ Kafra Employee ]</B>";
				mes "The Desert City, Morocc...";
				mes "It has lost it's former glory...";
				mes "Do not lose the dream in your heart right now.";
				callfunc "F_NvErase";
				close2;
				savepoint "morocc",150,99,1,1;
				warp "morocc",155,110;
				end;
			case 3:
				mes "<B>[ Kafra Employee ]</B>";
				mes "Hmm, you want to be an Archer?.";
				mes "Let's go to Payon.";
				mes "Let Freya bless you.";
				callfunc "F_NvErase";
				close2;
				savepoint "payon",70,100,1,1;
				warp "payon",166,67;
				end;
			case 4:
				mes "<B>[ Kafra Employee ]</B>";
				mes "The Port City, Alberta.";
				mes "Main city for all trades on the Midgard continent.";
				mes "If you want to experience other civilizations, you have to visit Alberta first.";
				callfunc "F_NvErase";
				close2;
				savepoint "alberta",30,232,1,1;
				warp "alberta",114,58;
				end;
			case 5:
				mes "<B>[ Kafra Employee ]</B>";
				mes "The City of Magic!";
				mes "Geffen Tower is looking nice standing in the middle of Geffen City.";
				mes "I will send you close to the Geffen fountain.";
				mes "May Goddess Freya bless you!.";
				callfunc "F_NvErase";
				close2;
				savepoint "geffen",119,37,1,1;
				warp "geffen",122,65;
				end;
			}
		case 2:
			mes "<B>[ Kafra Employee ]</B>";
			mes "I will explain about the Kafra Service.";
			mes "Which Service do you want to be";
			mes "informed about?";
			next;
			while(1) {
				set .@select, select("About Save:About Storage:About Teleport:About Cart:No More.");
				if (.@select == 5) {
					mes "<B>[ Kafra Employee ]</B>";
					mes "I hope you are satisfied with my explanation";
					mes "about the Kafra Service.";
					close;
				}
				callsub L_Services, .@select;
				next;
			}
		}
	}
	end;

L_Services:
	switch(getarg(0)) {
	case 1:
		mes "<B>[ Kafra Employee ]</B>";
		mes "When you are killed in a battle,";
		mes "you can revive in the location you saved with this service.";
		mes "If you save at a Kafra in each village";
		mes "You can revive in the village you saved.";
		next;
		mes "<B>[ Kafra Employee ]</B>";
		mes "Saved location is always where";
		mes "you saved last, and by using a Butterfly Wing,";
		mes "you can teleport directly to your saved point.";
		mes "So feel free to use this service.";
		break;
	case 2:
		mes "<B>[ Kafra Employee ]</B>";
		mes "Next is about using the Storage Service.";
		mes "You can store and retrieve your items in any town at your convenience.";
		next;
		mes "<B>[ Kafra Employee ]</B>";
		mes "It's unreasonable to carry all of your items with you when you don't need them right away.";
		mes "Please use our Storage and keep your items safe and secure.";
		next;
		mes "<B>[ Kafra Employee ]</B>";
		mes "Our convenient Storage Service";
		mes "is provided to our customers for a small fee which is different from town to town.";
		mes "However, you must be";
		mes "at least ^3355FFBasic Skill Level 6^000000";
		mes "to use the Storage.";
		next;
		mes "<B>[ Kafra Employee ]</B>";
		//mes "There are 3 different item sections of the Storage into which items are organized: Consumable, Equipment and Etc.";
		mes "There are 7 different item sections of the Storage into which items are organized: Consumable, Kafra (Item Mall), Armor, Weapons, Ammo, Cards, and Etc items.";
		next;

		// VIP System messages.
		if (MIN_STORAGE != MAX_STORAGE) {
			mes "<B>[ Kafra Employee ]</B>";
			mes "There are a maximum of "+MIN_STORAGE+" Inventory Slots in Kafra Storage, meaning you can have up to "+MIN_STORAGE+" different kinds of items in Storage.";
			next;
			mes "<B>[ Kafra Employee ]</B>";
			mes "If you have a Premium Subscription, the maximum Inventory Slots in Kafra Storage is "+MAX_STORAGE+"!";
			mes "Remember though, that in the case of Equipment, each item takes up one Inventory Slot.";
			mes "The maximum number of items that can be placed in Kafra Storage is 30,000.";
			next;
		} else {
			mes "<B>[ Kafra Employee ]</B>";
			mes "There are a maximum of "+MAX_STORAGE+" Inventory Slots in Kafra Storage, meaning you can have up to "+MAX_STORAGE+" different kinds of items in Storage.";
			next;
		}

		mes "<B>[ Kafra Employee ]</B>";
		mes "This Storage is shared by every character on one account.";
		mes "Storage Fees can be vary for each Town.";
		mes "So please keep that in mind when you are using our storage service.";
		break;
	case 3:
		mes "<B>[ Kafra Employee ]</B>";
		mes "We also provide you with a Teleport Service to make your trips easier when you are trying to move a long distance.";
		next;
		mes "<B>[ Kafra Employee ]</B>";
		mes "With our veteran teleporter's skill,";
		mes "your trip will be safer and more comfortable.";
		mes "Oh and remember that the places you can teleport to vary from town to town.";
		next;
		mes "<B>[ Kafra Employee ]</B>";
		mes "This is all for your never ending";
		mes "support. We are working hard to";
		mes "repay our dept to our customers.";
		break;
	case 4:
		mes "<B>[ Kafra Employee ]</B>";
		mes "The Kafra corporation provides Cart rental services";
		mes "to customers engaged in commercial business.";
		mes "Originally this cart rental service had been only provided";
		mes "by the merchant guild in Alberta";
		next;
		mes "<B>[ Kafra Employee ]</B>";
		mes "However, since we took over this service";
		mes "our merchant customers have been able to rent carts from almost anywhere.";
		mes "The cart rental service is available only for job classes engaged in commercial business";
		mes "such as ^4d4dffMerchants, Blacksmiths, Alchemists, and Super Novices^000000.";
		next;
		mes "<B>[ Kafra Employee ]</B>";
		mes "Of course you should learn the 'Pushcart' skill beforehand,";
		mes "otherwise you will not be able to rent a cart from us.";
		mes "The rental fee varies by town,";
		mes "please keep that in mind.";
		break;
	}
	return;
}
tg_1-1,152,238,5	duplicate(NvKafra)	Kafra Employee#nv1	4_F_KAFRA1
tg_1-2,152,238,5	duplicate(NvKafra)	Kafra Employee#nv2	4_F_KAFRA1
tg_1-3,152,238,5	duplicate(NvKafra)	Kafra Employee#nv3	4_F_KAFRA1
tg_1-4,152,238,5	duplicate(NvKafra)	Kafra Employee#nv4	4_F_KAFRA1
