//===== rAthena Script ======================================= 
//= Novice Training Grounds
//===== By: ================================================== 
//= Dr.Evil & MasterOfMuppets
//= Updated by Louis T Steinhil
//===== Current Version: ===================================== 
//= 2.5
//===== Compatible With: ===================================== 
//= rAthena Project; SVN 3422+
//===== Description: ========================================= 
//= [Official Conversion]
//= Novice Training Grounds
//===== Additional Comments: ================================= 
//= 1.7D Added confirm character name thing on receptionist. [Kisuka]
//= 1.8 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//=     NPCs are not backwards compatable, and will remove
//=     old variables used in the previous novice grounds.
//= 1.9 Fixed problem with Hanson's dialog. (bugreport:1703) [L0ne_W0lf]
//=     Fixed Entrance Guard clearing vars. (bugreport:1695)
//=     Added duplicate NPCs to combat training maps.
//= 2.0 Fixed savepoint not being changed sometimes. [L0ne_W0lf]
//= 2.1 Corrected spacing issue in welcome message. [L0ne_W0lf]
//= 2.2 Corrected Leo's endless dialog. [L0ne_W0lf]
//= 2.3 Added fix to Hanson from GM-windsor- and Saithis. [L0ne_W0lf]
//= 2.3a Correct a wrong variable name. (bugreport:2293) [Samuray22]
//= 2.4 Corrected an error in Hanson. (bugreport:2374) [L0ne_W0lf]
//= 2.5 Separated and improved Mage Guide [Louis T Steinhil]
//============================================================
-	script	Mage Guide#newbe10::NvMage	HIDDEN_NPC,{
	mes " ";
	setdialogsize(400, 300);
	setdialogpospercent(50, 50);
	if (job_novice_q < 15) {
		mes "<B>[ Mage Guild Member ]</B>";
		mes "You are still in the process of training with Instructor Brade.";
		close;
	}
	mes "<B>[ Mage Guild Member ]</B>";
	mes "What can I help you with?";
	next;
	switch(select("Tell me about Mages:I want to be a Mage:Give me a Quest:Cancel")) {
	case 1:
		mes "<B>[ Mage Guild Member ]</B>";
		mes "Mages are those who can use magic when they need to protect their friends.";
		mes "Mages can curse enemies using the elements of fire, water, earth and thunder magic.";
		next;
		mes "<B>[ Mage Guild Member ]</B>";
		mes "However they cannot equip other weapons but staffs and books.";
		mes "Because they are too delicate";
		mes "to carry heavy weapons.";
		next;
		mes "<B>[ Mage Guild Member ]</B>";
		mes "But they can cover this weakness";
		mes "by their remarkable magic skills.";
		mes "That is why many people want";
		mes "to become a Mage!";
		next;
		mes "<B>[ Mage Guild Member ]</B>";
		mes "Mages have to be in their guild for guild's power and honor.";
		mes "This is one of reasons why Mages are popular in any parties and guilds.";
		mes "Why don't you become a Mage?";
		next;
		mes "<B>[ Mage Guild Member ]</B>";
		mes "^8E2323Mages can transform to Wizards";
		mes "^8E2323or Sages as their second job^000000.";
		next;
		if (isequipped(2824) || countitem(2824)) {
			mes "<B>[ Mage Guild Member ]</B>";
			mes "I think you should test Mage skills before you decide what to do.";
			mes "I can provide you with our test skill registration form.";
			close;
		}
		mes "<B>[ Mage Guild Member ]</B>";
		mes "Do you want to register to experience how it is to be a Mage?";
		mes "Would you like to test Mage skills?";
		next;
		switch(select("I want to test Mage skills.:I don't want to test it.")) {
		case 1:
			mes "<B>[ Mage Guild Member ]</B>";
			mes "Here is the Mage Manual.";
			mes "I will take the other job registration forms and manuals from you.";
			next;
			callfunc "F_NvErase";
			getitem 2824,1; //Mage_Manual
			next;
			mes "<B>[ Mage Guild Member ]</B>";
			mes "After equipping the Manual, open your Skill menu by pressing ALT+S.";
			mes "Make sure to minimize your Skill tree by clicking the '-' button on the top right corner of the Skill Tree Window.";
			close;
		case 2:
			mes "<B>[ Mage Guild Member ]</B>";
			mes "Hum. Do you think so.";
			mes "Ok, I agree with you. Choosing a job is very important in our life.";
			mes "Just talk to me whenever you want to experience Mage skills.";
			close;
		}
	case 2:
		mes "<B>[ Mage Guild Member ]</B>";
		mes "You want to be a Mage?";
		mes "Ok, then, I will send you to the Mage Guild union in Geffen right away.";
		next;
		mes "<B>[ Mage Guild Member ]</B>";
		mes "Wait, have you checked your Job level?";
		mes "Do you want to complete your Novice training and go straight to the Mage Guild's union?";
		next;
		switch(select("I will think about it again.:Yes, complete my training.")) {
		case 1:
			mes "<B>[ Mage Guild Member ]</B>";
			mes "Please test what Mage skills are all about before you decide.";
			mes "We are always welcome to have you!";
			close;
		case 2:
			mes "<B>[ Mage Guild Member ]</B>";
			mes "That's great.";
			mes "Then I will end the Training Process and send you to the Mage Guild union in Geffen right away.";
			callfunc "F_NvErase",1;
			close2;
			savepoint "geffen",119,37,1,1;
			warp "geffen_in",163,98;
			end;
		}
	case 3:
		set .@quest, checkquest(7124);
		if (.@quest == -1) {
			mes "<B>[ Mage Guild Member ]</B>";
			mes "Huh? Quest?";
			mes "Hmm... What would be suited for you...?";
			mes "Would you like to hunt Lunatics by using Bolt skills?";
			next;
			mes "^4d4dffYou have received a Hunting quest from the Mage Guide.";
			mes "You can get more information about the quest from the quest window.^000000";
			setquest 7124;
			next;
			mes "<B>[ Mage Guild Member ]</B>";
			mes "You are able to use 'Fire Bolt' skill that is shown on the Mage manual.";
			mes "I will wait for you here.";
			mes "Good luck!";
			close;
		} else if (.@quest == 0 || .@quest == 1) {
			if (checkquest(7124,HUNTING) == 2) {
				mes "<B>[ Mage Guild Member ]</B>";
				mes "You have done an excellent job.";
				mes "So, did you find any interests about Mages?";
				mes "Alright. I will give something...";
				mes "Here are Novice Butterfly Wings, please take them.";
				completequest 7124;
				getitem 12324,30; //N_Butterfly_Wing
				getexp 5000,100;
				close;
			} else {
				mes "<B>[ Mage Guild Member ]</B>";
				mes "The quest is that you have to";
				mes "hunt Lunatics and bring 5 clovers.";
				close;
			}
		} else if (.@quest == 2) {
			mes "<B>[ Mage Guild Member ]</B>";
			mes "You have already completed the quest.";
			mes "Once you enter the main world, you can get various exciting quests there!";
			next;
			mes "<B>[ Mage Guild Member ]</B>";
			mes "This should be enough for your training.";
			close;
		} else {
			mes "<B>[ Mage Guild Member ]</B>";
			mes "I'm sorry but I don't have any quests to give you right now.";
			close;
		}
	case 4:
		mes "<B>[ Mage Guild Member ]</B>";
		mes "If you have any questions about Mages, feel free to ask me.";
		mes "I think you could be a intelligent Mage...";
		close;
	}
}
tg_1-1,145,230,5	duplicate(NvMage)	Mage Guide#nv1	2_F_MAGICMASTER
tg_1-2,145,230,5	duplicate(NvMage)	Mage Guide#nv2	2_F_MAGICMASTER
tg_1-3,145,230,5	duplicate(NvMage)	Mage Guide#nv3	2_F_MAGICMASTER
tg_1-4,145,230,5	duplicate(NvMage)	Mage Guide#nv4	2_F_MAGICMASTER
