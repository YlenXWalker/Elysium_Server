//===== rAthena Script =======================================
//= Clan masters
//===== Description: =========================================
//= The clan master NPCs that allow you to join and leave
//= one of the clans.
//===== Changelogs: ==========================================
//= 1.0 Initial release. [Louis T Steinhil]
//============================================================
angel_clan,83,150,4	script	[ Clan Master ]#1	4_EM_PROCELLA,{
	mes "<B>[ Reginleif ]</B>";
	mes "Hi, there! I'm Reginleif, master of the ^0000FFAngel Clan^000000";
	mes "What can I do for you?";
	setdialogsize(400, 300);
	setdialogpospercent(50, 50);
	next;
	switch( select( "Clan Information:Join.:Leave." ) ){
		case 1:
			switch( select( "What are Clans?:About the Angel Clan:Cancel." ) ){
				case 1:
					mes "<B>[ Reginleif ]</B>";
					mes "Join us, if no clan suits your taste.";
					mes "New members are given ^0000FFfree stat bonus^000000.";
					next;
					mes "<B>[ Reginleif ]</B>";
					mes "The ^0000FFAngel Clan^000000 is known for its serene disposition and unwavering support.";
					mes "Founded centuries ago by celestial beings, the clan offers sanctuary and camaraderie amidst the chaos of battle.";
					next;
					mes "<B>[ Reginleif ]</B>";
					mes "Support and Tank stats are offered as the name suggests, but not all members are Support or Tank classes.";
					mes "The clan celebrates diversity, welcoming adventurers of all classes to join their ranks.";
					next;
					mes "<B>[ Reginleif ]</B>";
					mes "Our headquarters, a grand citadel adorned with celestial symbols and glowing with ethereal light, serves as a haven for members.";
					mes "Here, we train under the guidance of elder angels, honing our skills and deepening our bonds of friendship.";
					next;
					mes "<B>[ Reginleif ]</B>";
					mes "In battle, Angel Clan members are a formidable force. With the blessings of the angels, we possess enhanced support and tank stats,";
					mes "enabling us to protect and heal our comrades with unmatched proficiency.";
					next;
					mes "<B>[ Reginleif ]</B>";
					mes "The Angel Clan believes in the power of unity and friendship, often organizing gatherings and events to strengthen our community bonds.";
					mes "Join us, and embark on a journey where support and strength go hand in hand.";
					next;
					mes "<B>[ Reginleif ]</B>";
					mes "In the Angel Clan, you'll not only find comrades in battle but also friends for life.";
					mes "Difference in variety creates the fun. These odd bunches never bore me in the party.";
					next;
					mes "<B>[ Reginleif ]</B>";
					mes "And who knows, you might meet your soul mate in the clan! Hahah!";
					close;
				case 2:
					mes "<B>[ Reginleif ]</B>";
					mes "The ^0000FFAngel Clan^000000 mainly for Support and Tank adventurers.";
					next;
					mes "<B>[ Reginleif ]</B>";
					mes "Eh, let me check the Clan manual. Ah, and we offer stat bonuses to new members:";
					mes "^0000FF<You'll be classified as an Angel>^000000.";
					mes "^0000FF<Resistance to all elements + 20%>^000000.";
					mes "^0000FF<Increase effectiveness of Heal + 100%>^000000!";
					mes "^0000FF<DEF & MDEF + 10%>^000000!";					
					mes "^0000FF<Damage received from Demon + 20%>^000000.";
					mes "^FF0000Warning: Your armor element is still neutral and will only change if you buff or equip an item that changes it^000000.";
					next;
					mes "<B>[ Reginleif ]</B>";
					mes "The Clans are not allowed to participate in the Siege; you should join a private guild instead for Siege.";
					mes "The Clans are for adventurers that hate being tied down.";
					next;
					mes "<B>[ Reginleif ]</B>";
					mes "Do you have a question?";
					break;
				case 3:
					break;
			}
			break;
		case 2:
			mes "<B>[ Reginleif ]</B>";
			mes "Oh, yeah? Good choice!";
			mes "Let's check the Clan policy then to begin admission procedures.";
			next;
			mes "<B>[ Reginleif ]</B>";
			mes "^0000FFClansmen^000000 cannot participate in the Siege.";
			mes "Eh, let me check the Clan manual. Ah, and we offer stat bonuses to new members:";
			mes "^0000FF<You'll be classified as an Angel>^000000.";
			mes "^0000FF<Resistant to all elements + 20%>^000000.";
			mes "^0000FF<Increase effectiveness of Heal + 100%>^000000!";
			mes "^0000FF<DEF & MDEF + 10%>^000000!";			
			mes "^0000FF<Damage received from Demon + 20%>^000000.";
			mes "^FF0000Warning: Your armor element is still neutral and will only change if you buff or equip an item that changes it^000000.";
			mes "Do you agree?";
			next;
			if( select( "Yes.:No." ) == 1 ){
				mes "<B>[ Reginleif ]</B>";
				mes "You can join and leave a Clan anytime, but if you don't login for ^0000FFmore than 2 weeks^000000, you'll be ^0000FFexpelled^000000.";
				mes "Do you promise you'll maintain good standing?";
				next;
				if( select( "Yes.:No." ) == 1 ){
					mes "<B>[ Reginleif ]</B>";
					mes "Good, write your name here.";
					next;
					input .@name$;
					mes "<B>[ Reginleif ]</B>";
					mes "^0000FF" + strcharinfo( 0 ) + "^000000..!";
					mes "Ugh, your penmanship needs work. Let me process your application.";
					next;
					if( getcharid( 2 ) ){
						mes "<B>[ Reginleif ]</B>";
						mes "Did you finish things up with your guild?";
						close;
					}else if( getcharid( 5 ) != 0 ){
						mes "<B>[ Reginleif ]</B>";
						mes "Eh, you're already in a Clan. Hey, this isn't funny.";
					}else{
						if (clan_join(ANGELCLAN)) {
							sc_start2 SC_ANGELCLAN, INFINITE_TICK, 0, ANGELCLAN;
							callfunc "TransformToRaceFantasy", 9;
							cutin "sham02.bmp", 2;
							mes "<B>[ Reginleif ]</B>";
							mes "Welcome to the ^0000FFAngel Clan^000000!";
							next;
							cutin "sham01.bmp", 2;
							mes "<B>[ Reginleif ]</B>";
							mes "You can review Clan info with ^0000FFCtrl+G^000000.";
							next;
							mes "<B>[ Reginleif ]</B>";
							mes "Don't pick fights with other clansmen. Be social.";
							mes "If you need help, you can always whisper me, okay? See you around!";
						} else
							mes "An error has occurred.";
					}
				}else{
					mes "<B>[ Reginleif ]</B>";
					mes "All right then. I'm just going to go have some fun.";
				}
			}else{
				mes "<B>[ Reginleif ]</B>";
				mes "Ah, do you need time to think about this? No problem.";
			}
			break;
		case 3:
			mes "<B>[ Reginleif ]</B>";
			mes "Why, did you find a better Clan?";
			mes "I'm disappointed, but it's your call. Let me process your withdrawal.";
			next;
			if( select( "Continue.:Cancel." ) == 1 ){
				if( getcharid( 5 ) == 0 ){
					cutin "sham04.bmp", 2;
					mes "<B>[ Reginleif ]</B>";
					mes "I can't let you leave--you're not even in my Clan!";
				}else{
					if (clan_leave()) {
						sc_end SC_ANGELCLAN;
						callfunc "TransformToPlayer";
						mes "<B>[ Reginleif ]</B>";
						mes "It's done.";
						mes "If you ever miss us, don't hesitate to come back, alright?";
					} else
						mes "An error has occurred.";
				}
			}
			break;
	}
	close2;
	cutin "", 255;
	end;

OnInit:
	//sc_start2 SC_ANGELCLAN, INFINITE_TICK, 0, ANGELCLAN, 10000, SCSTART_NOAVOID, getnpcid(0);
	setunitdata( getnpcid(0), UNPC_GROUP_ID, 100);
	setunittitle( getnpcid(0), "Reginleif" );	
	end;
}